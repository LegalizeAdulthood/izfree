<HTML>
<HEAD>
<!--
    izfree Tools for Windows Installer 1.1
    Copyright (C) 2001-2002 Pahvant Technologies, Inc.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
-->
<META name=VI60_defaultClientScript content=VBScript>
<HTA:APPLICATION NAVIGABLE="yes" ICON="izfree.ico" APPLICATIONNAME="izfree" />
<TITLE>izfree Tools for Windows Installer 1.1</TITLE>
<LINK REL="stylesheet" HREF="izfree.css">
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript" SRC="izfree.vbs"></SCRIPT>
<SCRIPT LANGUAGE="VBScript" TYPE="text/vbscript"><!--
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
option explicit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' MAIN_WIDTH, MAIN_HEIGHT
'
' Initial size of main window
'
const MAIN_WIDTH = 800
const MAIN_HEIGHT = 600

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' g_installer, g_wsh, g_fso, g_generator
'
' Objects that we are likely to need almost anywhere
'
dim g_installer, g_wsh, g_fso, g_generator

dim g_main_frame : set g_main_frame = window

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' g_database
'
' The currently open database, or nothing
'
dim g_database : set g_database = nothing
dim g_db_modified : g_db_modified = false

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' g_selected_tool
'
' Name of currently selected SPAN that represents the tool loaded in the
' IFRAME.
'
dim g_selected_tool

sub save_onClick
    on error resume next
    if (g_db_modified) then
        g_database.commit : check
    end if
    g_db_modified = false
    update_db_buttons
end sub

sub close_onClick
    save_modified
    set g_database = nothing
    g_db_modified = false
    document.all("database").innerText = ""
    set_tool "NewGUID"
    update_db_buttons
end sub

sub set_tool_class(tool, value)
    if (g_selected_tool = tool) then
        value = "selected"
    end if
    document.all(tool).className = value
end sub

sub update_db_buttons
    dim all : set all = document.all
    dim no_db : no_db = g_database is nothing
    all("open").disabled = not no_db
    all("new").disabled = not no_db
    all("close").disabled = no_db
    all("save").disabled = no_db or not g_db_modified
    dim modified : modified = ""
    if (g_db_modified) then modified = "*"
    all("modified").innerText = modified
    dim tool_class
    if (not no_db) then
        tool_class = "anchor"
    else
        tool_class = "invisible"
    end if
    set_tool_class "NewComponents", tool_class
    set_tool_class "PreviewDialogs", tool_class
    set_tool_class "ValidateTable", tool_class
    'all("ATLRegistration").className = tool_class
    'all("COMRegistration").className = tool_class
    'all("MergeModule").className = tool_class
    'all(""EditDialog").className = tool_class
end sub

sub open_onClick
    save_modified
    on error resume next
    dim file : file = browse_for_existing_file(INSTALLER_FILE_FILTER)
    if (file = "") then exit sub
    Err.Clear
    dim db : set db = _
        g_installer.OpenDatabase(file, msiOpenDatabaseModeTransact)
    if (Err.Number) then
        MsgBox "Couldn't open installer database:" & vbCRLF &_
            "Error " & Err.number & ", Source: " & Err.source & vbCRLF &_
            "Description: " & Err.Description, vbOK, _
            "Unexpected Error While Opening Database:"
        Err.Clear
        exit sub
    end if
    set_database file, db, false
    update_db_buttons
end sub

sub new_onClick
    save_modified
    window.showModalDialog "NewPackage.html", window, _
        "dialogWidth: 550px; dialogHeight: 400px"
    update_db_buttons
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub set_tool(tool)
    if (g_selected_tool <> "") then
        document.all(g_selected_tool).className = "anchor"
    end if
    g_selected_tool = tool
    document.all(tool).className = "selected"
    document.all("tool").src = tool & ".html"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' change_tool
'
' Switches the visual style of the currently selected tool and loads the
' corresponding file into the IFRAME.
'
sub change_tool
    set_tool window.event.srcElement.id
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' xxx_onClick
'
' Event handlers to load the corresponding tools.
'
sub NewGUID_onClick         : change_tool : end sub
sub NewPackage_onClick      : change_tool : end sub
sub NewComponents_onClick   : change_tool : end sub
sub PreviewDialogs_onClick  : change_tool : end sub
sub ATLCOM_onClick          : change_tool : end sub
sub MergeModule_onClick     : change_tool : end sub
sub EditDialog_onClick      : change_tool : end sub
sub ValidateTable_onClick   : change_tool : end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' window_onLoad
'
' Initialize the application.
'
sub window_onLoad
    on error resume next
    set g_wsh = check_object("WScript.Shell")
    set g_installer = check_object("WindowsInstaller.Installer")
    set g_fso = check_object("Scripting.FileSystemObject")
    set g_generator = check_object("Guidgen.Generator")
    set_tool "NewGUID"
end sub

sub save_modified
    if (g_db_modified) then
        dim yesno : yesno = MsgBox("Database '" &_
            document.all("database").innerText & "' has been modified." &_
            vbCRLF & "Do you want to save your changes?", vbYesNo, _
            "Confirm Saving Changed Database")
        if (yesno = vbYes) then
            g_database.commit
        end if
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' window_onUnload
'
' Cleans up resources allocated in the application init.
'
sub window_onUnload
    save_modified
    set g_database = nothing
    write_setting "", "Tool", g_selected_tool
    set g_installer = nothing
    set g_wsh = nothing
    set g_fso = nothing
    set g_generator = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' quit_onClick
'
' Close the window on quit.
'
sub quit_onClick : window.close : end sub

sub database_onKeyUp
    enable_db_buttons
end sub

sub database_onValueChange
    enable_db_buttons
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' resize the window immediately, don't wait for document to load.
window.resizeTo MAIN_WIDTH, MAIN_HEIGHT
--></SCRIPT>
</HEAD><BODY ID="Main"><TABLE WIDTH="100%" HEIGHT="100%" CELLPADDING="0">
<!--<TR VALIGN="top"><TD COLSPAN=2 WIDTH="100%">
<H1>izfree Tools for Windows Installer 1.1</H1>
<HR>
</TD></TR>-->
<TR><TD COLSPAN=2><FORM ID="dbForm">
<INPUT ID="open" TYPE="button" VALUE="Open...">
<INPUT ID="new" TYPE="button" VALUE="New...">
<INPUT ID="close" TYPE="button" VALUE="Close" DISABLED>
<INPUT ID="save" TYPE="button" VALUE="Save" DISABLED>
Database:&nbsp;<SPAN ID="database"></SPAN>&nbsp;<SPAN ID="modified"></SPAN>
</FORM>
</TD></TR>
<TR HEIGHT="100%" VALIGN="top">
<TD WIDTH="18%" HEIGHT="100%" VALIGN="top">
<HR>
<P><SPAN ID="NewGUID" CLASS="anchor">New GUID</SPAN></P>
<P><SPAN ID="NewPackageX" CLASS="invisible">New Package</SPAN></P>
<P><SPAN ID="NewComponents" CLASS="invisible">New Components</SPAN></P>
<P><SPAN ID="PreviewDialogs" CLASS="invisible">Preview Dialogs</SPAN></P>
<!-- COM registration extraction from an ATL .rgs file is in development. -->
<P CLASS="invisible"><SPAN ID="ATLRegistration" CLASS="anchor">ATL COM</SPAN></P>
<!-- Automatic COM registration extraction from a binary image is in development. -->
<P CLASS="invisible"><SPAN ID="COMRegistration" CLASS="anchor">COM Registration</SPAN></P>
<!-- Use Orca for merge modules -->
<P CLASS="invisible"><SPAN ID="MergeModule" CLASS="anchor">Merge Module</SPAN></P>
<!-- A dialog editor is in development. -->
<P CLASS="invisible"><SPAN ID="EditDialog" CLASS="anchor">Edit Dialog</SPAN></P>
<P><SPAN ID="ValidateTable" CLASS="invisible">Table Validation</SPAN></P>
<HR>
<P><INPUT TYPE="button" ID="quit" VALUE="Quit" NAME="quit"></P>
<HR>
</TD>
<TD WIDTH="85%" HEIGHT="100%">
<P ID="debug"></P>
<IFRAME NAME="tool" WIDTH="100%" ID="tool" HEIGHT="100%" APPLICATION="yes"></IFRAME>
</TD>
</TR>
</TABLE>
</BODY>
</HTML>
