<HTML>
<HEAD>
<META name=VI60_defaultClientScript content=VBScript>
<META NAME="GENERATOR" Content="Microsoft Visual Studio 6.0">
<TITLE>Create a New Installation Package</TITLE>
<LINK REL="stylesheet" HREF="izfree.css">
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript" SRC="izfree.vbs"></SCRIPT>
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript"><!--
option explicit

dim g_dest : g_dest = ""
dim g_source : g_source = ""

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' merge -- merge a source database into the destination database
'
' g_dest: destination database name
' g_source: source directory for template files
' db: source database to merge into g_dest
'
sub merge(db)
    dim dest_db : set dest_db = _
        installer().OpenDatabase(g_dest, msiOpenDatabaseModeTransact) : check
    dim src_db : set src_db = _
        installer().OpenDatabase(g_source & db, msiOpenDatabaseModeReadOnly) : check
    dim conflicts : conflicts = dest_db.Merge(src_db, "_MergeErrors")
    if (conflicts <> True) then
        conflicts = check
    end if
    if (conflicts <> 0) then
        MsgBox("Database merge countered conflicts.")
        set dest_db = nothing
        set src_db = nothing
        fso().DeleteFile(g_dest) : check
    else
        dest_db.Commit : check
        set dest_db = nothing
        set src_db = nothing
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' set_property
'
sub set_property(view, name, value)
    dim rec : set rec = installer().CreateRecord(2) : check
    rec.StringData(1) = name : rec.StringData(2) = value
    view.Execute rec : check
    set rec = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' create_package
'
' Given the inputs from the form, create a blank MSI package ready for
' editing.  The templates directory contains "schema.msi", "Sequence.msi"
' and "UISample.msi" as templates for the database schema, action sequences,
' and user interface, respectively.  The remaining arguments are the minimum
' required to pass full MSI validation in Orca on the resulting database.
'
sub create_package(template_dir, database, manufacturer, _
                   product_name, product_version, product_code, _
                   package_guid, upgrade_code)
    on error resume next
    g_source = template_dir
    if (right(g_source, 1) <> "\") then
        g_source = g_source & "\"
    end if
    g_dest = database
    if (Right(g_dest, 4) <> ".msi") then
        g_dest = g_dest & ".msi"
    end if

    if (fso().FileExists(g_dest)) then
        fso().DeleteFile g_dest : check
    end if
    fso().CopyFile g_source & "schema.msi", g_dest : check

    if (InStr(g_dest, " ")) then
        g_dest = chr(34) & g_dest & chr(34)
    end if
    merge "Sequence.msi"
    merge "UISample.msi"
    
    dim db : set db = _
        installer().OpenDatabase(g_dest, msiOpenDatabaseModeTransact) : check
    
    dim si : set si = db.SummaryInformation(9) 
    si.property(PID_TITLE) = "Installation Database" : check
    si.property(PID_SUBJECT) = product_name : check
    si.property(PID_AUTHOR) = manufacturer : check
    si.property(PID_COMMENTS) = "This installation database contains the " &_
        "logic and data needed to install " & product_name : check
    si.property(PID_REVNUMBER) = package_guid : check
    dim timestamp : timestamp = Now
    si.property(PID_LASTPRINTED) = timestamp  : check
    si.property(PID_CREATE_DTM) = timestamp : check
    si.property(PID_LASTSAVE_DTM) = timestamp : check
    si.property(PID_APPNAME) = "MSI Database Tools" : check
    si.persist : check
    set si = nothing
    
    dim view : set view = db.OpenView("INSERT INTO `Property`(" &_
            "`Property`,`Value`) VALUES (?,?)") : check
    set_property view, "Manufacturer", manufacturer
    set_property view, "ProductCode", product_code
    set_property view, "ProductName", product_name
    set_property view, "ProductVersion", product_version
    set_property view, "UpgradeCode", upgrade_code
    view.close : check
    set view = nothing

    db.commit : check    
    set db = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' is_hex
'
' Return true if the string contains only valid hexadecimal digits.
'
function is_hex(str)
    is_hex = true
    dim i : for i = 1 to len(str)
        if (instr("0123456789abcdef", lcase(mid(str,i,1))) = -1) then
            is_hex = false
            exit function
        end if
    next
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' {A80E420B-DF0E-4CB6-9649-338FEDF4792E}
' |        1    |    2    |    3       | 4
' 1234567890123456789012345678901234567890
'
' return true if the string is a valid guid
'
function is_guid(str)
    is_guid = (len(str) = 38) and _
        (left(str,1) = "{") and _
        (right(str,1) = "}") and _
        (mid(str,10,1) = "-") and _
        (mid(str,15,1) = "-") and _
        (mid(str,20,1) = "-") and _
        (mid(str,25,1) = "-") and _
        is_hex(mid(str,2,8)) and _
        is_hex(mid(str,11,4)) and _
        is_hex(mid(str,16,4)) and _
        is_hex(mid(str,21,4)) and _
        is_hex(mid(str,26,12))
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' update_create_package
'
' Update UI state of form based on current values.
'
sub update_create_package
    dim all : set all = document.forms("CreateForm").all
    dim tmp : tmp = is_guid(all("upgradeCode").value)
    all("create").disabled = not ( _
        (all("templateDir").value <> "") and _
        (all("database").value <> "") and _
        (all("manufacturer").value <> "") and _
        (all("productName").value <> "") and _
        (all("productVersion").value <> "") and _
        is_guid(all("productCode").value) and _
        is_guid(all("packageGuid").value) and _
        is_guid(all("upgradeCode").value) _
        )
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' create_onClick
'
' Validate the form contents and then create the package if all is OK
'
sub create_onClick
    dim all : set all = document.forms("CreateForm").all
    create_package all("templateDir").value, _
                        all("database").value, _
                        all("manufacturer").value, _
                        all("productName").value, _
                        all("productVersion").value, _
                        all("productCode").value, _
                        all("packageGuid").value, _
                        all("upgradeCode").value
    set all = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub write_tool_input(name)
    write_input "NewPackage", document.forms("CreateForm").all, name
end sub
sub read_tool_input(name, fallback)
    read_input "NewPackage", document.forms("CreateForm").all, name, _
        fallback
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onload
    read_tool_input "templateDir", _
        "C:\Program Files\Microsoft SDK\Samples\SysMgmt\Msi\Database" 
    read_tool_input "database", "test.msi"
    read_tool_input "manufacturer", ""
    read_tool_input "productName", ""
    read_tool_input "productVersion", "1.0"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onUnload
    write_tool_input "templateDir"
    write_tool_input "database"
    write_tool_input "manufacturer"
    write_tool_input "productName"
    write_tool_input "productVersion"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub    templateDir_onChange : update_create_package : end sub
sub       database_onChange : update_create_package : end sub
sub   manufacturer_onChange : update_create_package : end sub
sub    productName_onChange : update_create_package : end sub
sub productVersion_onChange : update_create_package : end sub
sub    productCode_onChange : update_create_package : end sub
sub    packageGuid_onChange : update_create_package : end sub

sub    templateDir_onKeyUp : update_create_package : end sub
sub       database_OnKeyUp : update_create_package : end sub
sub   manufacturer_onKeyUp : update_create_package : end sub
sub    productName_onKeyUp : update_create_package : end sub
sub productVersion_onKeyUp : update_create_package : end sub
sub    productCode_onKeyUp : update_create_package : end sub
sub    packageGuid_onKeyUp : update_create_package : end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub guid_on_click(button)
    set_guid_button "CreateForm", button
    update_create_package
end sub
sub guid1_onClick : guid_on_click "productCode" : end sub
sub guid2_onClick : guid_on_click "packageGuid" : end sub
sub guid3_onClick : guid_on_click "upgradeCode" : end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub help_onClick
    show_help "NewPackage"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub CreateForm_onAfterUpdate
    update_create_package
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub BrowseTemplateDir_onClick
    dim dir : dir = browse_for_existing_dir("Template directory:")
    if (dir <> "") then
        document.all("templateDir").value = dir
        update_create_package
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub BrowseDatabase_onClick
    dim dir : dir = browse_for_existing_dir("Output database directory:")
    if (dir <> "") then
        dim file : file = document.all("database").value
        dim slash : slash = instrrev(file, "\")
        if (slash > 0) then file = mid(file, slash+1)
        file = dir & "\" & file
        if (fso().FileExists(file)) then
            MsgBox("File '" & file & "' already exists.")
        else
            document.all("database").value = file
        end if
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub reset_onClick
    update_create_package
end sub
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
--></SCRIPT>
</HEAD>
<BODY>

<H3><A NAME="CreatePackage">Create a New Installation Package</A></H3>

<P><SPAN ID="help" CLASS="anchor">Help?</SPAN></P>

<FORM NAME="CreateForm"><TABLE>
<TR><TD>Template Directory:</TD>
<TD><INPUT NAME="templateDir" SIZE=50>
<INPUT TYPE="button" VALUE="..." ID="BrowseTemplateDir"></TD></TR>
<TR><TD>Database Filename:</TD>
<TD><INPUT NAME="database" SIZE=50>
<INPUT TYPE="button" VALUE="..." ID="BrowseDatabase"></TD></TR>
<TR><TD>Manufacturer:</TD>
<TD><INPUT NAME="manufacturer" SIZE=50></TD></TR>
<TR><TD>Product Name:</TD>
<TD><INPUT NAME="productName" SIZE=50></TD></TR>
<TR><TD>Product Version:</TD>
<TD><INPUT NAME="productVersion" SIZE=8></TD></TR>
<TR><TD>Product Code:</TD>
<TD><INPUT NAME="productCode" SIZE=41>
<INPUT TYPE="button" VALUE="New" id=guid1 name=guid1></TD></TR>
<TR><TD>Package GUID:</TD>
<TD><INPUT NAME="packageGuid" SIZE=41>
<INPUT TYPE="button" VALUE="New" id=guid2 name=guid2></TD></TR>
<TR><TD>Upgrade Code:</TD>
<TD><INPUT NAME="upgradeCode" SIZE=41>
<INPUT TYPE="button" VALUE="New" id=guid3 name=guid3></TD></TR>
</TABLE>
<INPUT TYPE="button" VALUE="Create Package" id="create" name="create" disabled>
<INPUT TYPE="reset" ID="reset" VALUE="Reset">
</FORM>
<P ID="debug"></P>
</BODY>
</HTML>
