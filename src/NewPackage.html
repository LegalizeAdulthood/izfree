<HTML APPLICATION="yes">
<HEAD>
<!--
    izfree Tools for Windows Installer
    Copyright (C) 2001-2002 Pahvant Technologies, Inc.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
-->
<META name=VI60_defaultClientScript content=VBScript>
<META NAME="GENERATOR" Content="Microsoft Visual Studio 6.0">
<TITLE>Create a New Installation Package</TITLE>
<LINK REL="stylesheet" HREF="izfree.css">
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript" SRC="izfree.vbs"></SCRIPT>
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript"><!--
option explicit

dim g_dest : g_dest = ""
dim g_source : g_source = ""
dim g_main_frame

dim g_db : set g_db = nothing

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' merge
'
' merge a source database into the destination database
' g_dest: destination database name
' g_source: source directory for template files
' db: source database to merge into g_dest
'
sub merge(db)
    dim dest_db : set dest_db = _
        installer().OpenDatabase(g_dest, msiOpenDatabaseModeTransact) : check
    dim src_db : set src_db = _
        installer().OpenDatabase(g_source & db, msiOpenDatabaseModeReadOnly) : check
    dim conflicts : conflicts = dest_db.Merge(src_db, "_MergeErrors")
    if (conflicts <> True) then
        conflicts = Err.Number
    end if
    if (conflicts <> 0) then
        MsgBox("Database merge countered conflicts.")
        set dest_db = nothing
        set src_db = nothing
        fso().DeleteFile(g_dest) : check
    else
        dest_db.Commit : check
        set dest_db = nothing
        set src_db = nothing
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' set_property
'
' Set a property value in the property table.
'
sub set_property(view, name, value)
    dim rec : set rec = installer().CreateRecord(2) : check
    rec.StringData(1) = name : rec.StringData(2) = value
    view.Execute rec : check
    set rec = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' si_prop
'
' Like set_property, but for summary information properties.
'
sub si_prop(si, name, value)
    si.property(name) = value : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' set_dir
'
' Set a record in the directory table
'
sub set_dir(view, dir, parent, defdir)
    on error resume next
    dim rec : set rec = installer().CreateRecord(3) : check
    rec.stringdata(1) = dir
    rec.stringdata(2) = parent
    rec.stringdata(3) = defdir
    view.execute rec : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' create_package
'
' Given the inputs from the form, create a blank MSI package ready for
' editing.  The templates directory contains "schema.msi", "Sequence.msi"
' and "UISample.msi" as templates for the database schema, action sequences,
' and user interface, respectively.  The remaining arguments are the minimum
' required to pass full MSI validation in Orca on the resulting database.
'
sub create_package(template_dir, database, manufacturer, _
                   product_name, product_version, product_code, _
                   package_guid, upgrade_code, target_root, target_dir)
    on error resume next
    g_source = template_dir
    if (right(g_source, 1) <> "\") then
        g_source = g_source & "\"
    end if
    g_dest = database
    if (Right(g_dest, 4) <> ".msi") then
        g_dest = g_dest & ".msi"
    end if

    ' start with schema
    if (fso().FileExists(g_dest)) then
        fso().DeleteFile g_dest : check
    end if
    fso().CopyFile g_source & "schema.msi", g_dest : check

    ' merge in sequence and UI tables
    if (InStr(g_dest, " ")) then
        g_dest = chr(34) & g_dest & chr(34)
    end if
    merge "Sequence.msi"
    merge "UISample.msi"
    
    ' now open the resulting file to substitute form values
    set g_db = _
        installer().OpenDatabase(g_dest, msiOpenDatabaseModeTransact) : check
    
    ' set the summary information properties
    dim si : set si = g_db.SummaryInformation(9) 
    si_prop si, PID_TITLE, "Installation Database"
    si_prop si, PID_TITLE, "Installation Database"
    si_prop si, PID_SUBJECT, product_name
    si_prop si, PID_AUTHOR, manufacturer
    si_prop si, PID_COMMENTS, "This installation database contains the " &_
        "logic and data needed to install " & product_name
    si_prop si, PID_REVNUMBER, package_guid
    dim timestamp : timestamp = Now
    si_prop si, PID_LASTPRINTED, timestamp 
    si_prop si, PID_CREATE_DTM, timestamp
    si_prop si, PID_LASTSAVE_DTM, timestamp
    si_prop si, PID_APPNAME, "izfree Tools for Windows Installer 1.1"
    si.persist : check
    set si = nothing
    
    ' set the required properties in the Property table
    dim view : set view = g_db.OpenView("INSERT INTO `Property`(" &_
            "`Property`,`Value`) VALUES (?,?)") : check
    set_property view, "Manufacturer", manufacturer
    set_property view, "ProductCode", product_code
    set_property view, "ProductName", product_name
    set_property view, "ProductVersion", product_version
    set_property view, "UpgradeCode", upgrade_code
    view.close : check
    
    ' give the new package a root install directory
    set view = g_db.OpenView("INSERT INTO `Directory`(`Directory`," &_
        "`Directory_Parent`,`DefaultDir`) VALUES (?,?,?)") : check
    set_dir view, "TARGETDIR", "", "SourceDir"
    set_dir view, target_root, "TARGETDIR", "."
    set_dir view, "APPDIR", target_root, wit_filename(target_dir) & ":."
    view.close : check
    
    ' give the new package a root feature
    set view = g_db.OpenView("insert into `Feature`(`Feature`," &_
        "`Feature_Parent`,`Title`,`Description`,`Display`,`Level`," &_
        "`Directory_`,`Attributes`) values ('All', '', 'All Features'," &_
        " 'All features in " & product_name & ".', 10, 1, 'APPDIR', 0)") : _
        check
    view.execute : check
    view.close : check
    
    ' save changes
    g_db.commit : check    
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' is_hex
'
' Return true if the string contains only valid hexadecimal digits.
'
function is_hex(str)
    is_hex = true
    dim i : for i = 1 to len(str)
        if (instr("0123456789ABCDEF", mid(str,i,1)) = -1) then
            is_hex = false
            exit function
        end if
    next
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' {A80E420B-DF0E-4CB6-9649-338FEDF4792E}
' |        1    |    2    |    3       | 4
' 1234567890123456789012345678901234567890
'
' return true if the string is a valid guid
'
function is_guid(str)
    is_guid = (len(str) = 38) and _
        (left(str,1) = "{") and (right(str,1) = "}") and _
        (mid(str,10,1) = "-") and (mid(str,15,1) = "-") and _
        (mid(str,20,1) = "-") and (mid(str,25,1) = "-") and _
        is_hex(mid(str,2,8)) and is_hex(mid(str,11,4)) and _
        is_hex(mid(str,16,4)) and is_hex(mid(str,21,4)) and _
        is_hex(mid(str,26,12))
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' update_create_package
'
' Update UI state of form based on current values.
'
sub update_create_package
    dim all : set all = document.forms("CreateForm").all
    dim tmp : tmp = is_guid(all("upgradeCode").value)
    all("create").disabled = not ( _
        (all("templateDir").value <> "") and _
        (all("database").value <> "") and _
        (all("manufacturer").value <> "") and _
        (all("productName").value <> "") and _
        (all("productVersion").value <> "") and _
        is_guid(all("productCode").value) and _
        is_guid(all("packageGuid").value) and _
        is_guid(all("upgradeCode").value) _
        )
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' create_onClick
'
' Validate the form contents and then create the package if all is OK
'
sub create_onClick
    dim all : set all = document.forms("CreateForm").all
    create_package all("templateDir").value, all("database").value, _
        all("manufacturer").value, all("productName").value, _
        all("productVersion").value, all("productCode").value, _
        all("packageGuid").value, all("upgradeCode").value, _
        all("targetRoots").value, all("targetDir").value
    set all = nothing
    set_database g_dest, g_db, false
    window.close
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' write_tool_input
' read_tool_input
'
' Persist tool inputs in the registry.
'
sub write_tool_input(name)
    write_input "NewPackage", document.forms("CreateForm").all, name
end sub
sub read_tool_input(name, fallback)
    read_input "NewPackage", document.forms("CreateForm").all, name, _
        fallback
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' dialog_load
'
' Setup things properly for a dialog by naming the window and getting
' the main frame from the window's dialog arguments.
'
sub dialog_load
    window.name = "Dialog"
    set g_main_frame = window.dialogArguments
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' window_onLoad
'
' Initialize the form inputs from registry tool settings.
'
sub window_onload
    dialog_load
    read_tool_input "templateDir", _
        "C:\Program Files\Microsoft SDK\Samples\SysMgmt\Msi\Database" 
    read_tool_input "database", "test.msi"
    read_tool_input "manufacturer", ""
    read_tool_input "productName", ""
    read_tool_input "productVersion", "1.0"
    
    load_opts document.all("targetRoots"), array("AdminToolsFolder", _
        "AppDataFolder", "CommonAppDataFolder", "CommonFilesFolder", _
        "CommonFiles64Folder", "DesktopFolder", "FavoritesFolder", _
        "FontsFolder", "LocalAppDataFolder", "MyPicturesFolder", _
        "PersonalFolder", "ProgramFilesFolder", "ProgramFiles64Folder", _
        "ProgramMenuFolder", "SendToFolder", "StartMenuFolder", _
        "StartupFolder", "SystemFolder", "System16Folder", "System64Folder", _
        "TempFolder", "TemplateFolder", "WindowsFolder", "WindowsVolume")
        
    read_tool_input "targetRoots", "ProgramFilesFolder"
    read_tool_input "targetDir", document.all("productName").value
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' dialog_unload
'
' Undo what dialog_load did.
'
sub dialog_unload
    set g_main_frame = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' window_onUnload
'
' Persist tool settings into the registry before unloading.
'
sub window_onUnload
    write_tool_input "templateDir"
    write_tool_input "database"
    write_tool_input "manufacturer"
    write_tool_input "productName"
    write_tool_input "productVersion"
    write_tool_input "targetRoots"
    write_tool_input "targetDir"
    dialog_unload
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' xxx_onChange, xxx_onKeyUp
'
' Checking to see if the create package button should be enabled.
'
sub    templateDir_onChange : update_create_package : end sub
sub       database_onChange : update_create_package : end sub
sub   manufacturer_onChange : update_create_package : end sub
sub    productName_onChange : update_create_package : end sub
sub productVersion_onChange : update_create_package : end sub
sub    productCode_onChange : update_create_package : end sub
sub    packageGuid_onChange : update_create_package : end sub
sub    upgradeCode_onChange : update_create_package : end sub

sub    templateDir_onKeyUp : update_create_package : end sub
sub       database_OnKeyUp : update_create_package : end sub
sub   manufacturer_onKeyUp : update_create_package : end sub
sub    productName_onKeyUp : update_create_package : end sub
sub productVersion_onKeyUp : update_create_package : end sub
sub    productCode_onKeyUp : update_create_package : end sub
sub    packageGuid_onKeyUp : update_create_package : end sub
sub    upgradeCode_onKeyUp : update_create_package : end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' guid_on_click
'
' Generate a new guid into a form input and possibly enable create
' package button.
'
sub guid_on_click(button)
    set_guid_button "CreateForm", button
    update_create_package
end sub
sub guid1_onClick : guid_on_click "productCode" : end sub
sub guid2_onClick : guid_on_click "packageGuid" : end sub
sub guid3_onClick : guid_on_click "upgradeCode" : end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' BrowseTemplateDir_onClick
'
' Browse for a directory containing database templates.
'
sub BrowseTemplateDir_onClick
    dim dir : dir = browse_for_existing_dir("Template directory:")
    if (dir <> "") then
        document.all("templateDir").value = dir
        update_create_package
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' BrowseDatabase_onClick
'
' Browse for a new output directory for the database.
'
sub BrowseDatabase_onClick
    dim dir : dir = browse_for_existing_dir("Output database directory:")
    if (dir <> "") then
        dim file : file = document.all("database").value
        dim slash : slash = instrrev(file, "\")
        if (slash > 0) then file = mid(file, slash+1)
        file = dir & "\" & file
        if (fso().FileExists(file)) then
            MsgBox("File '" & file & "' already exists.")
        else
            document.all("database").value = file
        end if
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' reset_onClick
'
' Update the state of the create package button after resetting the form.
'
sub reset_onClick
    update_create_package
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' cancel_onClick
'
' Close the window if the dialog is cancelled.
'
sub cancel_onClick
    window.close
end sub

sub browseTargetDir_onClick
    ' browse for directory
    ' compare paths of special directories against prefix of browsed dir
    ' if not special, then warn
    ' else
    ' create directory table entries from special folder to selected subdir
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' window_onHelp -- display CHM file
'
sub window_onHelp
    show_help "NewPackage"
end sub
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
--></SCRIPT>
</HEAD>
<BODY LEFTMARGIN=20 RIGHTMARGIN=20>
<FORM NAME="CreateForm">
<P><TABLE WIDTH=100%>
<TR><TD COLSPAN=2>
    <TABLE WIDTH=100%><TR><TD ALIGN="left"><H3>Create a New Installation Package</H3></TD>
    <TD ALIGN="right" VALIGN="top">Press F1 for Help.</TD></TR>
    </TABLE>
</TD></TR>
<TR><TD>Template Directory:</TD>
<TD><INPUT NAME="templateDir" SIZE=50>
<INPUT TYPE="button" VALUE="..." ID="BrowseTemplateDir"></TD></TR>
<TR><TD>Database Filename:</TD>
<TD><INPUT NAME="database" SIZE=50>
<INPUT TYPE="button" VALUE="..." ID="BrowseDatabase"></TD></TR>
<TR><TD>Manufacturer:</TD>
<TD><INPUT NAME="manufacturer" SIZE=50></TD></TR>
<TR><TD>Product Name:</TD>
<TD><INPUT NAME="productName" SIZE=50></TD></TR>
<TR><TD>Product Version:</TD>
<TD><INPUT NAME="productVersion" SIZE=8></TD></TR>
<TR><TD>Installation Directory:</TD>
<TD><SELECT ID="targetRoots"></SELECT>
\ <INPUT ID="targetDir" SIZE=25></TD></TR>
<TR><TD>Product Code:</TD>
<TD><INPUT NAME="productCode" SIZE=41>
<INPUT TYPE="button" VALUE="New" id=guid1 name=guid1></TD></TR>
<TR><TD>Package GUID:</TD>
<TD><INPUT NAME="packageGuid" SIZE=41>
<INPUT TYPE="button" VALUE="New" id=guid2 name=guid2></TD></TR>
<TR><TD>Upgrade Code:</TD>
<TD><INPUT NAME="upgradeCode" SIZE=41>
<INPUT TYPE="button" VALUE="New" id=guid3 name=guid3></TD></TR>
</TABLE></P>
<P>
<INPUT TYPE="button" VALUE="Create Package" id="create" name="create" disabled>
<INPUT TYPE="reset" ID="reset" VALUE="Reset Form">
<INPUT ID="cancel" TYPE="button" VALUE="Cancel">
</P>
</FORM>

<P ID="debug"></P>
</BODY>
</HTML>
