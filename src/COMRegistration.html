<HTML>
<HEAD>
<!--
    izfree Tools for Windows Installer
    Copyright (C) 2001-2002 Pahvant Technologies, Inc.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
-->
<META NAME=VI60_defaultClientScript CONTENT=VBScript>
<META NAME="GENERATOR" CONTENT="Microsoft Visual Studio 6.0">
<TITLE>Extract COM Registration Information</TITLE>
<LINK REL="stylesheet" HREF="izfree.css">
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript" SRC="izfree.vbs"></SCRIPT>
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript"><!--
option explicit
on error resume next

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim g_monitor

dim g_app_ids, g_classes, g_interfaces, g_type_libs, g_categories

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim g_main_frame : set g_main_frame = window.parent

function db()
    set db = g_main_frame.g_database
end function

function app_id_row(rec)
    dim row : set row = installer().CreateRecord(7)
    row.stringdata(1) = rec.AppId
    row.stringdata(2) = rec.RemoteServername
    row.stringdata(3) = rec.LocalService
    row.stringdata(4) = rec.ServiceParameters
    row.stringdata(5) = rec.DllSurrogate
    row.integerdata(6) = rec.ActivateAtStorage
    row.integerdata(7) = rec.RunAsInteractiveUser
    set app_id_row = row
end function

function class_row(rec)
    dim row : set row = installer().CreateRecord(13)
    row.stringdata(1) = rec.CLSID
    row.stringdata(2) = rec.context
    row.stringdata(3) = rec.component
    row.stringdata(4) = rec.ProgIdDefault
    row.stringdata(5) = rec.description
    row.stringdata(6) = rec.AppId
    row.stringdata(7) = rec.FileTypeMask
    row.stringdata(8) = rec.icon
    row.integerdata(9) = rec.IconIndex
    row.stringdata(10) = rec.DefInprocHandler
    row.stringdata(11) = rec.argument
    row.stringdata(12) = rec.feature
    row.integerdata(13) = rec.attributes
    set class_row = row
end function

function prog_id_row(rec)
    dim row : set row = installer().CreateRecord(6)
    row.stringdata(1) = rec.ProgId
    row.stringdata(2) = rec.Parent
    row.stringdata(3) = rec.Class
    row.stringdata(4) = rec.description
    row.stringdata(5) = rec.icon
    row.integerdata(6) = rec.IconIndex
    set prog_id_row = row
end function

function count_key(dict, key)
    if (not dict.exists(key)) then
        dict.add key, dict.Count+1
    end if
    count_key = dict(key)
end function

function guid_number(guids, key, start)
    guid_number = count_key(guids, _
        "{" & ucase(replace(mid(key, start, 36), "_", "-")) & "}")
end function

function unique_key(key)
    dim count : count = "..." & cstr(count_key(g_registry, key)) & "..."
    dim i : i = (72-len(count))/2
    unique_key = left(key, i) & count & right(key, i)
end function

function registry_row(rec)
    dim row : set row = installer().CreateRecord(6)
    dim key : key = rec.registry
    dim words : words = split(lcase(key), "_")
    if (words(0) = "hkcr") then
        select case words(1)
        case "appid"
            key = left(key, 11) & guid_number(g_app_ids, key, 12) &_
                mid(key, 48)
                    
        case "clsid"
            key = left(key, 11) & guid_number(g_classes, key, 12) &_
                mid(key, 48)
                    
        case "component"
            if (words(2) = "categories") then
                key = left(key, 26) & guid_number(g_categories, key, 27) &_
                    mid(key, 63)
            end if
                
        case "interface"
            key = left(key, 15) & guid_number(g_interfaces, key, 16) &_
                mid(key, 52)
            
        case "typelib"
            key = left(key, 13) & guid_number(g_type_libs, key, 14) &_
                mid(key, 50)
        end select
    end if
    if (len(key) > 72) then
        ' arbitrarily shrink keystring and hope for uniqueify with
        ' counter
        key = unique_key(key)
    end if
    row.stringdata(1) = key
    row.integerdata(2) = rec.root
    row.stringdata(3) = rec.key
    row.stringdata(4) = rec.name
    row.stringdata(5) = rec.value
    row.stringdata(6) = rec.component
    set registry_row = row
end function

function type_lib_row(rec)
    dim row : set row = installer().CreateRecord(13)
    row.stringdata(1) = rec.LibID
    if (rec.language = "" and rec.version <> "") then
        row.integerdata(2) = 0
    else
        row.integerdata(2) = rec.language
    end if
    row.stringdata(3) = rec.component
    row.integerdata(4) = rec.version
    row.stringdata(5) = rec.description
    row.stringdata(6) = rec.directory
    row.stringdata(7) = rec.feature
    row.integerdata(8) = rec.cost
    set type_lib_row = row
end function

function service_control_row(rec)
    dim row : set row = installer().CreateRecord(13)
    row.stringdata(1) = rec.ServiceControl
    row.stringdata(2) = rec.name
    row.integerdata(3) = rec.event
    row.stringdata(4) = rec.arguments
    row.integerdata(5) = rec.wait
    row.stringdata(6) = rec.component
    set service_control_row = row
end function

function service_install_row(rec)
    dim row : set row = installer().CreateRecord(13)
    row.stringdata(1) = rec.ServiceInstall
    row.stringdata(2) = rec.name
    row.stringdata(3) = rec.DisplayName
    row.integerdata(4) = rec.ServiceType
    row.integerdata(5) = rec.StartType
    row.integerdata(6) = rec.ErrorControl
    row.stringdata(7) = rec.LoadOrderGroup
    row.stringdata(8) = rec.dependencies
    row.stringdata(9) = rec.StartName
    row.stringdata(10) = rec.password
    row.stringdata(11) = rec.arguments
    row.stringdata(12) = rec.component
    row.stringdata(13) = rec.description
    set service_install_row = row
end function

sub save_app_id(table)
    on error resume next
    dim view : set view = db().openview("INSERT INTO `AppId`" &_
        "(`AppId`,`RemoteServerName`,`LocalService`,`ServiceParameters`," &_
        "`DllSurrogate`,`ActivateAtStorage`,`RunAsInteractiveUser`)" &_
        " VALUES (?,?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute app_id_row(rec) : check
    next
    view.close : check
end sub

sub save_class(table)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `Class`" &_
        "(`CLSID`,`Context`,`Component_`,`ProgId_Default`,`Description`," &_
        "`AppId_`,`FileTypeMask`,`Icon_`,`IconIndex`,`DefInprocHandler`," &_
        "`Argument`,`Feature_`,`Attributes`)" &_
        " VALUES (?,?,?,?,?,?,?,?,?,?,?,?,?)") : check
    dim component
    dim rec : for each rec in table
        dbgout "Component: " & rec.Component
        component = rec.Component
        view.execute class_row(rec) : check
    next
    view.close : check
    
    set view = exec_view(db(), "SELECT `Component_` FROM `File`")
    set rec = view.fetch
    do while not (rec is nothing)
        dbgout "Component_: " & rec.stringdata(1)
        set rec = view.fetch
    loop
    view.close : check
    
    set view = exec_view(db(), "SELECT `File`,`FileName` FROM `File`")
    set rec = view.fetch
    if (rec is nothing) then
        msgbox("Expected to find a file under component " & component)
    end if
    dim key_path
    do while (not rec is nothing)
        if (right(rec.stringdata(2), len(component)) = component) then
            key_path = rec.stringdata(1)
            set rec = nothing
        else
            set rec = view.fetch
        end if
    loop
    view.close : check
        
    set view = exec_view(db(), "UPDATE `Component` SET `KeyPath` = '" &_
        key_path & "' WHERE `Component` = '" & component & "'")
    view.close : check
end sub

sub save_prog_id(table)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `ProgId`" &_
        "(`ProgId`,`ProgId_Parent`,`Class_`,`Description`,`Icon_`," &_
        "`IconIndex`) VALUES (?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute prog_id_row(rec) : check
    next
    view.close : check
end sub

sub save_type_lib(table)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `TypeLib`" &_
        "(`LibID`,`Language`,`Component_`,`Version`,`Description`," &_
        "`Directory_`,`Feature_`,`Cost`) VALUES (?,?,?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute type_lib_row(rec) : check
    next
    view.close : check
end sub

sub scrape_registry_ids(db)
    on error resume next
    dim view : set view = exec_view(db, "SELECT `Root`,`Key`" &_
        " FROM `Registry`")
    dim rec : set rec = view.fetch
    do while not rec is nothing
        if (rec.integerdata(1) = 0) then 'HKCR
            dim words : words = split(rec.stringdata(2), "\")
            select case lcase(words(0))
            case "clsid" : count_key g_classes, words(1)
            case "appid" : count_key g_app_ids, words(1)
            case "interface" : count_key g_interfaces, words(1)
            case "typelib" : count_key g_type_libs, words(1)
            case "component categories" : count_key g_categories, words(1)
            end select
        end if
        set rec = view.fetch
    loop
end sub

sub save_registry(table)
    scrape_registry_ids db()
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `Registry`" &_
        "(`Registry`,`Root`,`Key`,`Name`,`Value`,`Component_`)" &_
        " VALUES (?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute registry_row(rec) : check
    next
    view.close : check
end sub

sub save_service_control(table)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `ServiceControl`" &_
        "(`ServiceControl`,`Name`,`Event`,`Arguments`,`Wait`,`Component_`)" &_
        " VALUES (?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute service_control_row(rec) : check
    next
    view.close : check
end sub

sub save_service_install(table)
    on error resume next
    dim view : set view = db().OpenView("insert into `ServiceInstall`" &_
        "(`ServiceInstall`,`Name`,`DisplayName`,`ServiceType`,`StartType`," &_
        "`ErrorControl`,`LoadOrderGroup`,`Dependencies`,`StartName`," &_
        "`Password`,`Arguments`,`Component_`,`Description`)" &_
        " VALUES (?,?,?,?,?,?,?,?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute service_install_row(rec)
    next
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub save_tables(mon)
    save_app_id mon.AppIdTable
    save_class mon.ClassTable
    save_prog_id mon.ProgIdTable
    save_type_lib mon.TypeLibTable
    save_registry mon.RegistryTable
    if (mon.Service) then
        save_service_control mon.ServiceControlTable
        save_service_install mon.ServiceInstallTable
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub extract_onClick
    dim file : file = document.all("server").value
    dim svc : svc = document.all("service").checked
    on error resume next
    Err.Clear
    g_monitor.Process file, svc
    if (Err) then
        MsgBox "Description: " & Err.description & vbCRLF &_
            "Source: " & Err.Source, vbOKOnly, "Error While Registering Server:"
        Err.Clear
        exit sub
    end if
    
    set g_app_ids = check_object("Scripting.Dictionary")
    set g_classes = check_object("Scripting.Dictionary")
    set g_interfaces = check_object("Scripting.Dictionary")
    set g_type_libs = check_object("Scripting.Dictionary")
    set g_categories = check_object("Scripting.Dictionary")
    
    save_tables g_monitor
    db_modified
end sub

sub write_tool_input(name)
    write_input "COMRegistration", document.forms("Register").all, name
end sub
sub read_tool_input(name, fallback)
    read_input "COMRegistration", document.forms("Register").all, name, _
        fallback
end sub

sub load_column(opts, table, column)
    on error resume next
    dim view : set view = exec_view(db(), "select `" & column &_
         "` from `" & table & "`")
    dim rec : set rec = view.fetch
    dim vals()
    do while (not rec is nothing)
        push vals, rec.stringdata(1)
        set rec = view.fetch
    loop
    view.close : check
    sort vals
    load_opts opts, vals
    opts.value = vals(0)
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onLoad
    set g_monitor = check_object("izfree.Monitor")
    
    dim keys : keys = array("HKEY_CLASSES_ROOT\AppID", _
        "HKEY_CLASSES_ROOT\CLSID", "HKEY_CLASSES_ROOT\TypeLib", _
        "HKEY_CLASSES_ROOT\Component Categories", "HKEY_CLASSES_ROOT", _
        "HKEY_CURRENT_USER\Software", "HKEY_CURRENT_USER", _
        "HKEY_LOCAL_MACHINE\Software", "HKEY_LOCAL_MACHINE", "HKEY_USERS")
    dim key : for each key in keys
        document.all("keys").options.add new_option(key)
        g_monitor.WatchKey key
    next
    read_tool_input "server", ""
    read_tool_input "service", 0
    
    load_column document.all("components"), "Component", "Component"
    load_column document.all("features"), "Feature", "Feature"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onUnload
    set g_monitor = nothing
    write_tool_input "server"
    write_tool_input "service"
end sub
--></SCRIPT>
</HEAD>
<BODY>
<P><FORM NAME="Register">
<TABLE WIDTH=100%>
    <TR>
        <TD ALIGN="right"><STRONG>Path to COM server:</STRONG></TD>
        <TD><INPUT ID="server" TYPE="text" SIZE=50 VALUE=""></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Service EXE</STRONG></TD>
        <TD><INPUT NAME="service" TYPE="checkbox"></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Component:</STRONG></TD><TD><SELECT ID="components"></SELECT></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Feature:</STRONG></TD>
        <TD><SELECT ID="features"></SELECT></TD>
    </TR>
    <TR>
        <TD ALIGN="right" VALIGN="top"><STRONG>Monitored Registry keys:</STRONG></TD>
        <TD>
            <SELECT ID="keys" STYLE="width: 100%" SIZE=12></SELECT><BR>
            <INPUT ID="addKey" TYPE="button" VALUE="Add...">
        </TD>
    </TR>
<TR><TD COLSPAN=2><INPUT ID="extract" TYPE="button" VALUE="Go"></TD></TR>
</TABLE>
</FORM></P>

<P ID="debug"></P>
</BODY>
</HTML>
