<HTML>
<HEAD>
<!--
    izfree Tools for Windows Installer
    Copyright (C) 2001-2002 Pahvant Technologies, Inc.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
-->
<META NAME=VI60_defaultClientScript CONTENT=VBScript>
<META NAME="GENERATOR" CONTENT="Microsoft Visual Studio 6.0">
<TITLE>Extract COM Registration Information</TITLE>
<LINK REL="stylesheet" HREF="izfree.css">
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript" SRC="izfree.vbs"></SCRIPT>
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript"><!--
option explicit
on error resume next

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' PAGE_xxx
'
' Constants for the pages within the wizard sequence that adds new
' components.
'
const PAGE_SERVER = 1
const PAGE_COMMIT = 2
const PAGE_LAST = 2

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' g_page
'
' Current page number being displayed within the sequence.
'
dim g_page : g_page = 1

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' g_monitor -- izfree.Monitor object
'
dim g_monitor

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' GUID dictionaries used to construct legible primary keys for the
' Registry table
'
dim g_app_ids, g_classes, g_interfaces, g_type_libs, g_categories, g_registry

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' g_main_frame -- globals frame
dim g_main_frame : set g_main_frame = window.parent

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' db -- database accessor
'
function db()
    set db = g_main_frame.g_database
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' count_key -- use a dictionary to count unique keys
'
function count_key(dict, key)
    if (not dict.exists(key)) then
        dict.add key, dict.Count+1
        count_key = dict.Count
    else
        count_key = dict(key)
    end if
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' guid_number -- replace a guid with a unique number
'
function guid_number(guids, key, start)
    guid_number = count_key(guids, _
        "{" & ucase(replace(mid(key, start, 36), "_", "-")) & "}")
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' unique_key -- squish a key down into 72 columns or less but keep it unique
'
function unique_key(key)
    dim count : count = "..." & cstr(count_key(g_registry, key)) & "..."
    dim i : i = (72-len(count))/2
    unique_key = left(key, i) & count & right(key, i)
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' xxx_row
'
' Take a row from izMonitor and package it as a Windows Installer database
' record object
'
function app_id_row(rec)
    dim row : set row = installer().CreateRecord(7)
    row.stringdata(1) = rec.AppId
    row.stringdata(2) = rec.RemoteServername
    row.stringdata(3) = rec.LocalService
    row.stringdata(4) = rec.ServiceParameters
    row.stringdata(5) = rec.DllSurrogate
    row.integerdata(6) = rec.ActivateAtStorage
    row.integerdata(7) = rec.RunAsInteractiveUser
    set app_id_row = row
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
function class_row(rec)
    dim row : set row = installer().CreateRecord(13)
    row.stringdata(1) = rec.CLSID
    row.stringdata(2) = rec.context
    row.stringdata(3) = rec.component
    row.stringdata(4) = rec.ProgIdDefault
    row.stringdata(5) = rec.description
    row.stringdata(6) = rec.AppId
    row.stringdata(7) = rec.FileTypeMask
    row.stringdata(8) = rec.icon
    row.integerdata(9) = rec.IconIndex
    row.stringdata(10) = rec.DefInprocHandler
    row.stringdata(11) = rec.argument
    row.stringdata(12) = rec.feature
    row.integerdata(13) = rec.attributes
    set class_row = row
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
function prog_id_row(rec)
    dim row : set row = installer().CreateRecord(6)
    row.stringdata(1) = rec.ProgId
    row.stringdata(2) = rec.Parent
    row.stringdata(3) = rec.Class
    row.stringdata(4) = rec.description
    row.stringdata(5) = rec.icon
    row.integerdata(6) = rec.IconIndex
    set prog_id_row = row
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
function service_control_row(rec)
    dim row : set row = installer().CreateRecord(13)
    row.stringdata(1) = rec.ServiceControl
    row.stringdata(2) = rec.name
    row.integerdata(3) = rec.event
    row.stringdata(4) = rec.arguments
    row.integerdata(5) = rec.wait
    row.stringdata(6) = rec.component
    set service_control_row = row
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
function service_install_row(rec)
    dim row : set row = installer().CreateRecord(13)
    row.stringdata(1) = rec.ServiceInstall
    row.stringdata(2) = rec.name
    row.stringdata(3) = rec.DisplayName
    row.integerdata(4) = rec.ServiceType
    row.integerdata(5) = rec.StartType
    row.integerdata(6) = rec.ErrorControl
    row.stringdata(7) = rec.LoadOrderGroup
    row.stringdata(8) = rec.dependencies
    row.stringdata(9) = rec.StartName
    row.stringdata(10) = rec.password
    row.stringdata(11) = rec.arguments
    row.stringdata(12) = rec.component
    row.stringdata(13) = rec.description
    set service_install_row = row
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' registry_row
'
' Scrape out a GUID if possible and reduce to a simple unique counter
' to avoid registry keys that are too long but are still readable.
' If they key is still too long anyway, then uniqueify the key but
' squish to 72 characters or less.
'
function registry_row(rec)
    dim row : set row = installer().CreateRecord(6)
    dim key : key = rec.registry
    dim words : words = split(lcase(key), "_")
    if (words(0) = "hkcr") then
        select case words(1)
        case "appid"
            key = left(key, 11) & guid_number(g_app_ids, key, 12) &_
                mid(key, 48)
                    
        case "clsid"
            key = left(key, 11) & guid_number(g_classes, key, 12) &_
                mid(key, 48)
                    
        case "component"
            if (words(2) = "categories") then
                key = left(key, 26) & guid_number(g_categories, key, 27) &_
                    mid(key, 63)
            end if
                
        case "interface"
            key = left(key, 15) & guid_number(g_interfaces, key, 16) &_
                mid(key, 52)
            
        case "typelib"
            key = left(key, 13) & guid_number(g_type_libs, key, 14) &_
                mid(key, 50)
        end select
    end if
    if (len(key) > 72) then
        ' arbitrarily shrink keystring and for uniqueify with a counter
        key = unique_key(key)
    end if
    row.stringdata(1) = key
    row.integerdata(2) = rec.root
    row.stringdata(3) = rec.key
    row.stringdata(4) = rec.name
    row.stringdata(5) = rec.value
    row.stringdata(6) = rec.component
    set registry_row = row
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' type_lib_row
'
' Generate a row in the TypeLib table from the monitor data.  Force any
' non-zero help directory for the type lib to "APPDIR".  A more sophisticated
' approach would try to determine the Directory table entry corresponding
' to the directory scraped from the registry.
' 
function type_lib_row(rec)
    dim row : set row = installer().CreateRecord(13)
    row.stringdata(1) = rec.LibID
    if (rec.language = "" and rec.version <> "") then
        row.integerdata(2) = 0
    else
        row.integerdata(2) = rec.language
    end if
    row.stringdata(3) = rec.component
    row.integerdata(4) = rec.version
    row.stringdata(5) = rec.description
    if (rec.directory <> "") then
        row.stringdata(6) = "APPDIR"
    else
        row.stringdata(6) = ""
    end if
    row.stringdata(7) = rec.feature
    row.integerdata(8) = rec.cost
    set type_lib_row = row
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_xxx
'
' Create a view to insert rows into the table and iterate over the
' containers to stuff each scraped row into the database
'
sub save_app_id(table)
    on error resume next
    dim view : set view = db().openview("INSERT INTO `AppId`" &_
        "(`AppId`,`RemoteServerName`,`LocalService`,`ServiceParameters`," &_
        "`DllSurrogate`,`ActivateAtStorage`,`RunAsInteractiveUser`)" &_
        " VALUES (?,?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute app_id_row(rec) : check
    next
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub save_prog_id(table)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `ProgId`" &_
        "(`ProgId`,`ProgId_Parent`,`Class_`,`Description`,`Icon_`," &_
        "`IconIndex`) VALUES (?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute prog_id_row(rec) : check
    next
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub save_registry(table)
    scrape_registry_ids db()
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `Registry`" &_
        "(`Registry`,`Root`,`Key`,`Name`,`Value`,`Component_`)" &_
        " VALUES (?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute registry_row(rec) : check
    next
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub save_service_control(table)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `ServiceControl`" &_
        "(`ServiceControl`,`Name`,`Event`,`Arguments`,`Wait`,`Component_`)" &_
        " VALUES (?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute service_control_row(rec) : check
    next
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub save_service_install(table)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `ServiceInstall`" &_
        "(`ServiceInstall`,`Name`,`DisplayName`,`ServiceType`,`StartType`," &_
        "`ErrorControl`,`LoadOrderGroup`,`Dependencies`,`StartName`," &_
        "`Password`,`Arguments`,`Component_`,`Description`)" &_
        " VALUES (?,?,?,?,?,?,?,?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute service_install_row(rec)
    next
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub save_type_lib(table)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `TypeLib`" &_
        "(`LibID`,`Language`,`Component_`,`Version`,`Description`," &_
        "`Directory_`,`Feature_`,`Cost`) VALUES (?,?,?,?,?,?,?,?)") : check
    dim rec : for each rec in table
        view.execute type_lib_row(rec) : check
    next
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_class
'
' The class table is a slightly different.  First go looking through the
' file table for a file 
sub save_class(table, byval server)
    on error resume next
    dim view : set view = db().OpenView("INSERT INTO `Class`" &_
        "(`CLSID`,`Context`,`Component_`,`ProgId_Default`,`Description`," &_
        "`AppId_`,`FileTypeMask`,`Icon_`,`IconIndex`,`DefInprocHandler`," &_
        "`Argument`,`Feature_`,`Attributes`)" &_
        " VALUES (?,?,?,?,?,?,?,?,?,?,?,?,?)") : check
    dim component : component = table(0).Component
    dim rec : for each rec in table
        view.execute class_row(rec) : check
    next
    view.close : check
    
    set view = exec_view(db(), "SELECT `File`,`FileName` FROM `File`" &_
        " WHERE `Component_` = '" & component & "'")
    set rec = view.fetch
    if (rec is nothing) then
        msgbox "I expected to find a file under component " & component &_
            "." & vbCRLF & vbCRLF &_
            "The component will need a key path set to pass validation.", _
            vbOKOnly, "Warning: KeyPath for Component Not Set"
    else            
        ' find the file whose filename is the same as the server
        dim key_path
        dim whack : whack = instrrev(server, "\")
        if (whack > 0) then server = mid(server, whack+1)
        do while (not rec is nothing)
            if (right(rec.stringdata(2), len(server)) = server) then
                key_path = rec.stringdata(1)
                set rec = nothing
            else
                set rec = view.fetch
            end if
        loop
        view.close : check
        
        set view = exec_view(db(), "UPDATE `Component` SET `KeyPath` = '" &_
            key_path & "' WHERE `Component` = '" & component & "'")
    end if
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' scrape_registry_ids
'
' Go through the existing rows in the registry table to scrape out
' GUIDs needed to build unique primary keys for new rows written into
' the registry table.
'
sub scrape_registry_ids(db)
    on error resume next
    dim view : set view = exec_view(db, "SELECT `Root`,`Key`,`Registry`" &_
        " FROM `Registry`")
    dim rec : set rec = view.fetch
    do while not rec is nothing
        if (rec.integerdata(1) = 0) then 'HKCR
            dim words : words = split(rec.stringdata(2), "\")
            select case lcase(words(0))
            case "clsid" : count_key g_classes, words(1)
            case "appid" : count_key g_app_ids, words(1)
            case "interface" : count_key g_interfaces, words(1)
            case "typelib" : count_key g_type_libs, words(1)
            case "component categories" : count_key g_categories, words(1)
            end select
        end if
        count_key g_registry, rec.stringdata(3)
        set rec = view.fetch
    loop
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_tables -- save out the scraped database table rows
'
sub save_tables(mon)
    if (mon.AppIdTable.Count > 0) then save_app_id mon.AppIdTable
    if (mon.ClassTable.Count > 0) then save_class mon.ClassTable, mon.File
    if (mon.ProgIdTable.Count > 0) then save_prog_id mon.ProgIdTable
    if (mon.TypeLibTable.Count > 0) then save_type_lib mon.TypeLibTable
    if (mon.RegistryTable.Count > 0) then save_registry mon.RegistryTable
    if (mon.Service) then
        if (mon.ServiceControlTable.Count > 0) then
            save_service_control mon.ServiceControlTable
        end if
        if (mon.ServiceInstallTable.Count > 0) then
            save_service_install mon.ServiceInstallTable
        end if
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' accum_msg -- helper for reporting table results
'
sub accum_msg(table, name, byref msg)
    if (table.Count > 0) then
        if (msg <> "") then
            msg = msg & ", "
        end if
        msg = msg & table.Count & " " & name & " rows"
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub write_tool_input(name)
    write_input "COMRegistration", document.forms("Register").all, name
end sub
sub read_tool_input(name, fallback)
    read_input "COMRegistration", document.forms("Register").all, name, _
        fallback
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' load_column -- load a <SELECT> object from a column of a database table
'
sub load_column(opts, table, column)
    on error resume next
    if db() is nothing then exit sub
    dim view : set view = exec_view(db(), "SELECT `" & column &_
         "` FROM `" & table & "`")
    dim rec : set rec = view.fetch
    dim vals()
    do while (not rec is nothing)
        push vals, rec.stringdata(1)
        set rec = view.fetch
    loop
    view.close : check
    sort vals
    load_opts opts, vals
    opts.value = vals(0)
end sub

sub keys_update
    dim sel : set sel = document.all("keys")
    if (sel.options.length = 0) or (sel.selectedIndex < 0) then
        document.all("deleteKey").disabled = true
        document.all("moveUp").disabled = true
        document.all("moveDown").disabled = true
    else
        document.all("deleteKey").disabled = false
        document.all("moveUp").disabled = (sel.selectedIndex = 0)
        document.all("moveDown").disabled = _
            (sel.selectedIndex = sel.options.length-1)
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub addKey_onClick
    dim key : key = InputBox("Enter a registry key name:", _
        "Add a Registry Key", "")
    if (key <> "") then
        document.all("keys").options.add new_option(key)
    end if
    keys_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub deleteKey_onClick
    dim sel : set sel = document.all("keys")
    dim idx : idx = sel.selectedIndex
    sel.remove idx
    if (idx = sel.options.length) then idx = idx-1
    sel.selectedIndex = idx
    keys_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub moveUp_onClick
    dim sel : set sel = document.all("keys")
    dim idx : idx = sel.selectedIndex
    dim val : set val = sel(idx)
    sel.remove idx
    sel.add val, idx-1
    sel.selectedIndex = idx-1
    keys_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub moveDown_onClick
    dim sel : set sel = document.all("keys")
    dim idx : idx = sel.selectedIndex
    dim val : set val = sel(idx)
    sel.remove idx
    sel.add val, idx+1
    sel.selectedIndex = idx+1
    keys_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub keys_onChange
    dim sel : set sel = document.all("keys")
    if (sel.selectedIndex >= 0) then
        keys_update
    end if
end sub

function dump_keys(mon)
    dim msg(), i
    if (mon.AppIdTable.Count > 0) then
        push msg, "AppId Entries:"
        for i = 1 to mon.AppIdTable.Count
            push msg, "    " & mon.AppIdTable(i).AppId
        next
        push msg, ""
    end if
    if (mon.ClassTable.Count > 0) then
        push msg, "Class Entries:"
        for i = 1 to mon.ClassTable.Count
            push msg, "    " & mon.ClassTable(i).CLSID
        next
        push msg, ""
    end if
    if (mon.ProgIdTable.Count > 0) then
        push msg, "ProgId Entries:"
        for i = 1 to mon.ProgIdTable.Count
            push msg, "    " & mon.ProgIdTable(i).ProgId
        next
        push msg, ""
    end if
    if (mon.TypeLibTable.Count > 0) then
        push msg, "TypeLib Entries:"
        for i = 1 to mon.TypeLibTable.Count
            push msg, "    " & mon.TypeLibTable(i).LibID
        next
        push msg, ""
    end if
    if (mon.RegistryTable.Count > 0) then
        push msg, "Registry Entries:"
        for i = 1 to mon.RegistryTable.Count
            push msg, "    " & replace(mon.RegistryTable(i).Registry, "_", " ")
        next
        push msg, ""
    end if
    if (mon.ServiceControlTable.Count > 0) then
        push msg, "ServiceControl Entries:"
        for i = 1 to mon.ServiceControl.Count
            push msg, "    " & mon.ServiceControlTable(i).ServiceControl
        next
        push msg, ""
    end if
    if (mon.ServiceInstallTable.Count > 0) then
        push msg, "ServiceInstall Entries:"
        for i = 1 to mon.ServiceInstall.Count
            push msg, "    " & mon.ServiceInstallTable(i).ServiceInstall
        next
    end if
    dump_keys = join(msg, vbCRLF) & vbCRLF
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' register_server
'
' Ask a self-registering COM server to register itself and scrape all
' its registry information out into tables.
'
function register_server
    register_server = true
    dim file : file = document.all("server").value
    dim svc : svc = document.all("service").checked
    dim component : component = document.all("components").value
    dim feature : feature = document.all("features").value
    
    g_monitor.ClearKeys
    dim key : for each key in document.all("keys").options
        g_monitor.WatchKey key.value
    next
    
    on error resume next
    Err.Clear
    g_monitor.Process file, svc, component, feature
    if (Err) then
        MsgBox "Description: " & Err.description & vbCRLF &_
            "Source: " & Err.Source & vbCRLF & vbCRLF &_
            "No action has been taken on the open database.", vbOKOnly, _
            "Error While Registering Server:"
        Err.Clear
        register_server = false
    else
        dim msg : msg = ""
        accum_msg g_monitor.AppIdTable, "AppId", msg
        accum_msg g_monitor.ClassTable, "Class", msg
        accum_msg g_monitor.ProgIdTable, "ProgId", msg
        accum_msg g_monitor.TypeLibTable, "TypeLib", msg
        accum_msg g_monitor.RegistryTable, "Registry", msg
        accum_msg g_monitor.ServiceControlTable, "ServiceControl", msg
        accum_msg g_monitor.ServiceInstallTable, "ServiceInstall", msg
        if (msg <> "") then
            dim whack : whack = instrrev(file, "\")
            if (whack > 0) then file = mid(file, whack+1)
            if (svc) then
                msg = "Service '" & file & "' yielded " & msg & "."
            else
                msg = "File '" & file & "' yielded " & msg & "."
            end if
            document.all("rows").value = msg & vbCRLF & dump_keys(g_monitor)
        else
            document.all("rows").value = "No COM registration information " &_
                "was found in file " & file & "."
        end if
    end if
end function

function record_server    
    set g_app_ids = check_object("Scripting.Dictionary")
    set g_classes = check_object("Scripting.Dictionary")
    set g_interfaces = check_object("Scripting.Dictionary")
    set g_type_libs = check_object("Scripting.Dictionary")
    set g_categories = check_object("Scripting.Dictionary")
    set g_registry = check_object("Scripting.Dictionary")
    
    save_tables g_monitor
    db_modified
    
    record_server = true
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onLoad
    set g_monitor = check_object("izfree.Monitor")
    
    read_tool_input "server", ""
    read_tool_input "service", 0
    
    load_opts document.all("keys"), array("HKEY_CLASSES_ROOT\AppID", _
        "HKEY_CLASSES_ROOT\CLSID", "HKEY_CLASSES_ROOT\TypeLib", _
        "HKEY_CLASSES_ROOT\Component Categories", "HKEY_CLASSES_ROOT", _
        "HKEY_CURRENT_USER\Software", "HKEY_CURRENT_USER", _
        "HKEY_LOCAL_MACHINE\Software", "HKEY_LOCAL_MACHINE", "HKEY_USERS")
    load_column document.all("components"), "Component", "Component"
    load_column document.all("features"), "Feature", "Feature"

    keys_update
    back_next_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onUnload
    write_tool_input "server"
    write_tool_input "service"
    set g_monitor = nothing
end sub

sub browseServer_onClick
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' cancel_onClick
'
' Cancel the actions of the wizard sequence and reset to the first page.
'
sub cancel_onClick
    document.all("page" & g_page).className = "invisible"
    g_page = 1
    document.all("page" & g_page).className = ""
    back_next_update
end sub
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' finish_onClick
'
' Do all the remaining steps, taking the default action, and finish up
' the wizard sequence.
'
sub finish_onClick
    document.all("page" & g_page).className = "invisible"
    dim ok : ok = true
    do while (g_page < PAGE_LAST) and ok
        ok = do_step()
        g_page = g_page+1
    loop
    if (do_step()) then
        g_page = 1
        document.all("page" & g_page).className = ""
    end if
    back_next_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' next_onClick
'
' Move forward one step in the wizard sequence.
'
sub next_onClick
    if (do_step()) then
        if (g_page >= 1) then
            document.all("page" & g_page).className = "invisible"
        end if
        g_page = g_page + 1
        if (g_page <= PAGE_LAST) then
            document.all("page" & g_page).className = ""
        end if
    end if
    back_next_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' back_onClick
'
' Move backward one step in the wizard sequence.
'
sub back_onClick
    if (g_page <= PAGE_LAST) then
        document.all("page" & g_page).className = "invisible"
    end if
    g_page = g_page - 1
    if (g_page >= 1) then
        document.all("page" & g_page).className = ""
   end if
   undo_step
   back_next_update 
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' do_step
'
' Do a step in the wizard sequence.
'
function do_step
    select case g_page
    case PAGE_SERVER:   do_step = register_server
    case PAGE_LAST:     do_step = record_server
    end select
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' undo_step
'
' Undo a step in the wizard sequence.
'
sub undo_step
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' back_next_update
'
' Depending on the page and the inputs on each page, determine whether
' or not the back, next, cancel and finish buttons should be enabled.
'
sub back_next_update
    select case g_page
    case PAGE_SERVER:
        document.all("back").disabled = true
        document.all("next").disabled = false
        document.all("cancel").disabled = true
        document.all("finish").disabled = false
        
    case PAGE_COMMIT:
        document.all("back").disabled = false
        document.all("next").disabled = true
        document.all("cancel").disabled = false
        document.all("finish").disabled = false
    case else
        MsgBox("Unexpected case on back_next_update")
    end select
end sub

--></SCRIPT>
</HEAD>
<BODY>
<FORM NAME="Register"><TABLE WIDTH=100%>
<TR ID="page1"><TD COLSPAN=3>
<P><STRONG>1. Select COM Server:</STRONG></P>
<P><TABLE WIDTH=100%>
    <TR>
        <TD ALIGN="right"><STRONG>Path to COM server:</STRONG></TD>
        <TD><INPUT ID="server" TYPE="text" SIZE=50 VALUE="">
        <INPUT ID="browseServer" TYPE="button" VALUE="..."></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Service EXE</STRONG></TD>
        <TD><INPUT NAME="service" TYPE="checkbox"></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Component:</STRONG></TD><TD><SELECT ID="components"></SELECT></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Feature:</STRONG></TD>
        <TD><SELECT ID="features"></SELECT></TD>
    </TR>
    <TR>
        <TD ALIGN="right" VALIGN="top">
            <P><STRONG>Monitored Registry keys:</STRONG></P>
            <P><INPUT ID="addKey" TYPE="button" VALUE="Add..."><BR>
            <INPUT ID="deleteKey" TYPE="button" VALUE="Delete" DISABLED><BR>
            <INPUT ID="moveUp" TYPE="button" VALUE="Move Up" DISABLED><BR>
            <INPUT ID="moveDown" TYPE="button" VALUE="Move Down" DISABLED></P>
        </TD>
        <TD>
            <SELECT ID="keys" STYLE="width: 100%" SIZE=12></SELECT>
        </TD>
    </TR>
</TABLE>
</TD></TR>

<TR ID="page2" CLASS="invisible"><TD COLSPAN=3>
<P><STRONG>2. Confirm Database Rows Added.</STRONG></P>

<P><TEXTAREA ID="rows" STYLE="width: 100%" ROWS=20></TEXTAREA></P>
</TD></TR>

<P><TR>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="back" TYPE="button" VALUE="< Back" DISABLED></TD>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="next" TYPE="button" VALUE="Next >" DISABLED></TD>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="cancel" TYPE="button" VALUE="Cancel" DISABLED>
<INPUT ID="finish" TYPE="button" VALUE="Finish" DISABLED></TD>
</TR></P>
</TABLE></FORM>

</FORM></P>

<P ID="debug"></P>
</BODY>
</HTML>
