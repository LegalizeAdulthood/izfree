<HTML>
<HEAD>
<!--
    izfree Tools for Windows Installer 1.1
    Copyright (C) 2001-2002 Pahvant Technologies, Inc.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
-->
<META name=VI60_defaultClientScript content=VBScript>
<META NAME="GENERATOR" Content="Microsoft Visual Studio 6.0">
<TITLE>Dialog Tools</TITLE>
<LINK REL="stylesheet" HREF="izfree.css">
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript" SRC="izfree.vbs"></SCRIPT>
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript">
<!--
option explicit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim g_preview : set g_preview = nothing
dim g_current_dialog : g_current_dialog = ""
dim g_main_frame : set g_main_frame = window.parent

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
function db
    set db = g_main_frame.g_database
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
function dialog_dic(db)
    set dialog_dic = db_dic(db, "Dialog", "Dialog")
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub load_wizards(list, db)
    ' get dialogs from database
    dim dlgs : set dlgs = dialog_dic(db)

    ' load all wizard starting points from the UI sequence table
    dim view : set view = exec_view(db, "select `Action` from " &_
        "`InstallUISequence` where `Sequence` >= 0 order by `Sequence`")
    dim rec : set rec = view.fetch
    dim ui_dialogs()
    do while (not rec is nothing)
        dim dlg : dlg = rec.stringdata(1)
        if dlgs.exists(dlg) then
            push ui_dialogs, dlg
        end if
        set rec = view.fetch
    loop
    view.close : check
    set view = nothing

    sort ui_dialogs
    
    ' build selection list
    load_opts list.options, ui_dialogs
    
    dim all_dlgs() : redim all_dlgs(dlgs.count-1)
    dim i : i = 0
    for each dlg in dlgs.keys
        all_dlgs(i) = dlg
        i = i + 1
    next
    sort all_dlgs
    load_opts document.all("dialogs").options, all_dlgs
    document.all("dialogs").value = list.value
    dialogs_onChange
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub clone_dialog_rows(table, dest_query, parent, clone, column)
    on error resume next
    dim src_query : src_query = "SELECT * FROM `" & table & "` WHERE `" &_
        table & "`.`" & column & "` = '" & parent & "'"
    dim src_view : set src_view = db().OpenView(src_query) : check
    src_view.execute : check
    dim records()
    dim rec : set rec = src_view.fetch : check
    do while (not rec is nothing)
        rec.stringdata(1) = clone
        push records, rec
        set rec = src_view.fetch
    loop
    src_view.close : check
    set src_view = nothing
    
    dest_query = "INSERT INTO `" & table & "`" & dest_query
    dim dest_view : set dest_view = db().OpenView(dest_query) : check
    dim i : for i = 0 to num_records-1
        dest_view.execute records(i) : check
    next
    dest_view.close
    set dest_view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' clone_dialog_table
'
sub clone_dialog_table(parent, clone)
    clone_dialog_rows "Dialog", _
        "(`Dialog`,`HCentering`,`VCentering`,`Width`,`Height`," &_
        "`Attributes`,`Title`,`Control_First`,`Control_Default`," &_
        "`Control_Cancel`) VALUES (?,?,?,?,?,?,?,?,?,?)", _
        parent, clone, "Dialog"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' clone_control_table
'
sub clone_control_table(parent, clone)
    clone_dialog_rows "Control", _
        "(`Dialog_`,`Control`,`Type`,`X`,`Y`,`Width`,`Height`," &_
        "`Attributes`,`Property`,`Text`,`Control_Next`,`Help`) " &_
        "VALUES (?,?,?,?,?,?,?,?,?,?,?,?)", _
        parent, clone, "Dialog_"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' clone_control_condition_table
'
sub clone_control_condition_table(parent, clone)
    clone_dialog_rows "ControlCondition", _
        "(`Dialog_`,`Control_`,`Action`,`Condition`) VALUES (?,?,?,?)", _
        parent, clone, "Dialog_"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' clone_control_event_table
'
sub clone_control_event_table(parent, clone)
    clone_dialog_rows "ControlEvent", _
        "(`Dialog_`,`Control_`,`Event`,`Argument`,`Condition`,`Ordering`) " &_
        "VALUES (?,?,?,?,?,?)", _
        parent, clone, "Dialog_"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' clone_event_mapping_table
'
sub clone_event_mapping_table(parent, clone)
    clone_dialog_rows "EventMapping", _
        "(`Dialog_`,`Control_`,`Event`,`Attribute`) VALUES (?,?,?,?)", _
        parent, clone, "Dialog_"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' update_dialog_buttons
'
' Update form UI state based on current values
'
sub update_dialog_buttons
    with document.forms("Wizards")
        dim disabled : disabled = (db() is nothing) or _
            (g_preview is nothing) or (.all("dialogs").selectedIndex = -1)
        .all("preview").disabled = disabled
        .all("clone").disabled = disabled
        .all("clear").disabled = disabled or (g_current_dialog = "")
    end with
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub write_tool_input(name)
    write_input "PreviewDialogs", document.forms("Wizards").all, name
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub read_tool_input(name, fallback)
    read_input "PreviewDialogs", document.forms("Wizards").all, name, _
        fallback
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
function get_ui_action(db, seq)
    on error resume next
    dim view : set view = db.OpenView("select `Action` from " &_
        "`InstallUISequence` where `Sequence` = " & seq) : check
    view.execute : check
    dim rec : set rec = view.fetch
    dim dlg_name : dlg_name = ""
    if (not rec is nothing) then
        dlg_name = rec.stringdata(1)
    end if
    view.close : check
    if (dlg_name <> "") then
        set view = db.OpenView("select `Dialog` from `Dialog` where " &_
            "`Dialog` = '" & dlg_name & "'") : check
        view.execute : check
        set rec = view.fetch
        if (rec is nothing) then
            dlg_name = ""
        end if
        view.close : check
    end if
    get_ui_action = dlg_name
end function

sub select_option(sel, item)
    dim i : for i = 0 to sel.options.length-1
        if (sel.options(i).value = item) then
            sel.selectedIndex = i
            exit sub
        end if
    next
end sub
-->
</SCRIPT>
<SCRIPT ID=clientEventHandlersVBS LANGUAGE=vbscript>
<!--
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' clear_onClick
'
' Clear any previewed dialog.
'
sub clear_onClick
    g_current_dialog = ""
    g_preview.viewdialog(g_current_dialog)
    update_dialog_buttons
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' clone_onClick
'
sub clone_onClick
    on error resume next
    dim list : set list = document.forms("Wizards").all("dialogs")
    if (list.selectedIndex <> -1) then
        on_clear_preview
        dim parent_dlg : parent_dlg = list.options(list.selectedindex).value
        dim num : num = 0
        do while (num < len(parent_dlg)) and _
                (instr("0123456789", mid(parent_dlg, len(parent_dlg)-num, 1)))
            num = num+1
        loop
        dim clone
        if (num > 0) then
            clone = left(parent_dlg, num)
            num = cint(mid(tmp, num))
        else
            clone = parent_dlg
        end if
        num = num + 1
        clone = InputBox("Clone " & parent_dlg & " to child named:", _
            "Clone Dialog", clone & num)
        if (clone <> "") then
            clone_dialog_table parent_dlg, clone
            clone_control_table parent_dlg, clone
            clone_control_condition_table parent_dlg, clone
            clone_control_event_table parent_dlg, clone
            clone_event_mapping_table parent_dlg, clone
            db_modified
            load_dialogs list, db()
        end if
    end if
end sub

sub preview_dialog(dlg)
    if (g_current_dialog <> "") and (g_current_dialog <> dlg) then
        g_current_dialog = dlg
        g_preview.viewDialog g_current_dialog
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub dialogs_onChange
    dim sel : set sel = document.all("dialogs")
    dim dlg : dlg = sel.value
    if (sel.selectedIndex >= 0) then
        dim vu : set vu = db().OpenView("select `Control_`,`Argument` " &_
            "from `ControlEvent` where `Event` = 'NewDialog' " &_
            "and `Dialog_` = '" & dlg & "'")
        vu.execute : check
        dim rec : set rec = vu.fetch

        dim evts()
        do while (not rec is nothing)
            push evts, rec.stringdata(1) & " -> " & rec.stringdata(2)
            set rec = vu.fetch
        loop
        vu.close : check

        load_opts document.all("events").options, evts
    end if
    document.all("ordering").innerText = ""
    document.all("condition").innerText = ""

    update_dialog_buttons
    preview_dialog dlg
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub help_onClick
    show_help "PreviewDialogs"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' preview_onClick
'
' Preview the dialog selected in the listbox.
'
sub preview_onClick
    dim list : set list = document.forms("Wizards").all("dialogs")
    dim selected : selected = list.selectedIndex
    if (selected <> -1) then
        g_current_dialog = list.options(selected).value
        g_preview.viewDialog g_current_dialog
    end if
    update_dialog_buttons
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub events_onChange
    dim seq : set seq = document.all("events")
    if (seq.selectedIndex >= 0) then
        dim dlg : dlg = selected_option(document.all("dialogs"))
        dim evt : evt = split(selected_option(seq), " ")
        dim view : set view = exec_view(db(), "select `Condition`,`Ordering`" &_
            " from `ControlEvent` where `Dialog_` = '" & dlg &_
            "' and `Control_` = '" & evt(0) & "' and `Argument` = '" & evt(2) & "'")
        dim rec : set rec = view.fetch
        if (rec is nothing) then
            MsgBox "Unexpected empty record for query."
        else
            document.all("condition").innerText = null_str(rec, 1)
            document.all("ordering").innerText = null_int(rec, 2)
        end if
        view.close : check
        set rec = nothing
        set view = nothing
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub events_onDblClick
    dim dlg : dlg = split(selected_option(document.all("events")), " ")(2)
    document.all("dialogs").value = dlg
    dialogs_onChange
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub load_standard_dialogs(all, db)
    all("fatalErrorDlg").innerText = get_ui_action(db, -3)
    all("userExitDlg").innerText = get_ui_action(db, -2)
    all("exitDlg").innerText = get_ui_action(db, -1)
    all("errorDlg").innerText = get_property(db, "ErrorDialog")
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onLoad
    dim all : set all = document.forms("Wizards").all
    load_standard_dialogs all, db()
    load_wizards all("actions"), db()
    set g_preview = db().enableuipreview
    set all = nothing
    update_dialog_buttons
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onBeforeUnload
    if (not g_preview is nothing) then
        g_preview.viewdialog("")
        set g_preview = nothing
    end if
end sub

sub set_dialog_text(id)
    dim dlg : dlg = document.all(id).innerText
    document.all("dialogs").value = dlg
    dialogs_onChange
end sub

sub fatalErrorDlg_onClick   : set_dialog_text "fatalErrorDlg"   : end sub
sub userExitDlg_onClick     : set_dialog_text "userExitDlg"     : end sub
sub exitDlg_onClick         : set_dialog_text "exitDlg"         : end sub
sub errorDlg_onClick        : set_dialog_text "errorDlg"        : end sub

sub actions_onChange
    document.all("dialogs").value = document.all("actions").value
    dialogs_onChange
end sub
-->
</SCRIPT>
</HEAD>
<BODY>

<P><FORM NAME="Wizards">
<TABLE WIDTH=100%>
    <TR><TD>
        <INPUT TYPE="button" VALUE="Preview" disabled id=preview>
        <INPUT TYPE="button" VALUE="Clear" disabled id=clear>
        <INPUT TYPE="button" VALUE="Clone..." disabled id=clone>
    </TD></TR>
    <TR><TD><TABLE>
        <TR><TD COLSPAN=2><STRONG>UI Sequence Dialogs:</STRONG><BR>
            <SELECT ID="actions" NAME="actions" SIZE=1 CLASS="listBox"></SELECT>
        </TD></TR>
        <TR><TD>
            <STRONG>Dialogs:</STRONG><BR>
            <SELECT ID="dialogs" NAME="dialogs" CLASS="listBox" SIZE=12></SELECT>
        </TD><TD VALIGN="top">
            <STRONG>Standard Dialogs:</STRONG><BR>
                <TABLE>
                    <TR><TD ALIGN="right"><STRONG>Fatal Error (-3):</STRONG></TD>
                    <TD ID="fatalErrorDlg" CLASS="anchor"></TD></TR>
                    <TR><TD ALIGN="right"><STRONG>User Exit (-2):</STRONG></TD>
                    <TD ID="userExitDlg" CLASS="anchor"></TD></TR>
                    <TR><TD ALIGN="right"><STRONG>Exit (-1):</STRONG></TD>
                    <TD ID="exitDlg" CLASS="anchor"></TD></TR>
                    <TR><TD ALIGN="right"><STRONG>ErrorDialog Property:</STRONG></TD>
                    <TD ID="errorDlg" CLASS="anchor"></TD></TR>
                </TABLE>
        </TD></TR>
        <TR><TD><STRONG>Events:</STRONG><BR>
            <SELECT ID="events" CLASS="listBox" SIZE=6></SELECT>
        </TD><TD VALIGN="top"><STRONG>Event Detail:</STRONG><BR>
            <STRONG>Ordering:</STRONG>&nbsp;<SPAN ID="ordering"></SPAN><BR>
            <STRONG>Condition:</STRONG>&nbsp;<SPAN ID="condition"></SPAN><BR>
        </TD></TR>
    </TABLE></TD></TR>
</TABLE>
</FORM></P>

</BODY>
</HTML>
