<HTML>
<HEAD>
<!--
    izfree Tools for Windows Installer
    Copyright (C) 2001-2002 Pahvant Technologies, Inc.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
-->
<META name=VI60_defaultClientScript content=VBScript>
<META NAME="GENERATOR" Content="Microsoft Visual Studio 6.0">
<TITLE>Scan Directory for Components</TITLE>
<LINK REL="stylesheet" HREF="izfree.css">
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript" SRC="izfree.vbs"></SCRIPT>
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript"><!--
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
option explicit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim g_main_frame : set g_main_frame = window.parent

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' PAGE_xxx
'
' Constants for the pages within the wizard sequence that adds new
' components.
'
const PAGE_DIRECTORY = 1
const PAGE_COMPONENTS = 2
const PAGE_FEATURES = 3
const PAGE_COMMIT = 4
const PAGE_LAST = 4 

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' g_page
'
' Current page number being displayed within the sequence.
'
dim g_page : g_page = 1

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' files()
' dirs()
' components()
'
' Arrays holding information about files, directories and components to
' be added to the database
'
dim num_files : dim files()
const fil_dir = 0
const fil_name = 1
const fil_component = 2
const fil_size = 3
const fil_dims = 3

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim num_dirs : dim dirs()
const dir_dir = 0
const dir_name = 1
const dir_parent = 2
const dir_default = 3
const dir_dims = 3

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim num_components : dim components()
const cmp_name = 0
const cmp_dir = 1
const cmp_file_list = 2
const cmp_dims = 2
dim num_dir_components

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_component_table
'
' Write Component table into database.
'
sub save_component_table(db)
    dim view : set view = db.openview("INSERT INTO `Component`(" &_
        "`Component`,`ComponentId`,`Directory_`,`Attributes`," &_
        "`Condition`,`KeyPath`) VALUES (?,?,?,0,'','')") : check
    dim digits : digits = sig_digits(num_components)
    dim i : for i = 0 to num_components-1
        dim rec : set rec = installer().CreateRecord(3) : check
        rec.stringdata(1) = components(cmp_name, i)
        rec.stringdata(2) = new_guid()
        rec.stringdata(3) = dirs(dir_name, components(cmp_dir, i))
        view.execute rec : check : if err.number then exit sub
        set rec = nothing
    next
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' database_key
'
' Replace any illegal characters in a database key string with _'s.
' Legal characters are letters, digits, '.' and '_'.
'
function database_key(str)
    dim i : for i = 0 to len(str)
        dim char : char = ucase(mid(str, i, 1))
        if not (((char >= "A") and (char <= "Z")) or _
                ((char >= "0") and (char <= "9")) or _
                (char = ".") or (char = "_")) then
            if (i <> 0) then
                str = left(str, i-1) & "_" & mid(str, i+1)
            else
                str = "_" & mid(str, i+1)
            end if
        end if
    next
    database_key = str
end function

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_file_table
'
' Write File table into database.
'
sub save_file_table(db)
    dim view : set view = db.OpenView("INSERT INTO `File`(" & _
            "`File`,`Component_`,`FileName`,`FileSize`,`Version`," & _
            "`Language`,`Attributes`,`Sequence`) " & _
            "VALUES (?,?,?,?,'','',0,1)") : check

    dim digits : digits = sig_digits(num_files)
    dim i : for i = 0 to num_files-1
        dim rec : set rec = installer().CreateRecord(4) : check
        rec.stringdata(1) = "f" & right("000000" & i, digits) & "_" &_
            database_key(files(fil_name, i))
        rec.stringdata(2) = components(cmp_name, files(fil_component, i))
        rec.stringdata(3) = wit_filename(files(fil_name, i))
        rec.integerdata(4) = files(fil_size, i)
        view.execute rec : check : if err.number then exit sub
        set rec = nothing
    next
    
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_directory_table
'
' Write Directory table into database.
'
sub save_directory_table(db, root)
    dim view : set view = db.openview("INSERT INTO `Directory`(" &_
        "`Directory`,`Directory_Parent`,`DefaultDir`) VALUES (?,?,?)") : check
    dim i : for i = 0 to num_dirs-1
        if (dirs(dir_name, i) = "TARGETDIR") then
        else
            dim rec : set rec = installer().CreateRecord(3) : check
            rec.stringdata(1) = dirs(dir_name, i)
            rec.stringdata(2) = dirs(dir_parent, i)
            rec.stringdata(3) = dirs(dir_default, i)
            view.execute rec : check : if err.number then exit sub
        end if
        set rec = nothing
    next
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_media_table
'
' Write Media table into database.  We are writing in the minimal it
' takes for us to get by with a valid installation using an uncompressed
' source.  This is a constant record, with the sequence numbers of all
' files being set to 1.  For the purposes of the new components tool,
' we assume that if the table has any records, then we don't need to add
' this minimal record.
'
sub save_media_table(db)
    dim view : set view = exec_view(db, "select * from `Media`")
    dim rec : set rec = view.fetch
    view.close : check
    set view = nothing
    if (rec is nothing) then
        set view = exec_view(db, "INSERT INTO `Media`(`DiskId`," &_
        "`LastSequence`,`DiskPrompt`,`Cabinet`,`VolumeLabel`,`Source`) " &_
        "VALUES (1,1,'','','','')") : check
        view.close : check
        set view = nothing
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_feature_table
'
' Write Feature table into database.  Place the new features under the
' feature given by root.
'
sub save_feature_table(db, root)
    ' get largest display value to start counting for new features
    dim view : set view = exec_view(db, _
        "select `Display` from `Feature` order by `Display`")
    dim display : display = 0
    dim rec : set rec = view.fetch
    do while (not rec is nothing)
        if (not rec.isnull(1)) then
            dim num : num = cint(rec.integerdata(1)/2)
            if (num > display) then display = num
        end if
        set rec = view.fetch
    loop
    view.close : check
    if (display = 0) then display = 100 else display = display+1
    
    set view = db.openview("INSERT INTO `Feature`(`Feature`," &_
        "`Feature_Parent`,`Title`,`Description`,`Display`,`Level`," &_
        "`Directory_`,`Attributes`) VALUES (?,?,?,?,?,?,?,0)") : check
    dim i : for i = 0 to num_components-1
        set rec = installer().CreateRecord(7) : check
        rec.stringdata(1) = components(cmp_name, i) 'feature
        rec.stringdata(2) = root                    'parent
        rec.stringdata(3) = components(cmp_name, i) 'title
        rec.stringdata(4) = components(cmp_name, i) 'description
        rec.integerdata(5) = 2*(i+display)          'display
        rec.integerdata(6) = 1                      'level
        rec.stringdata(7) = "" 'dirs(dir_name, components(cmp_dir, i)) 'directory
        view.execute rec : check
        set rec = nothing
    next
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_feature_components_table
'
' Write FeatureComponents table into database.
'
sub save_feature_components_table(db)
    dim view : set view = db.openview("INSERT INTO `FeatureComponents`(" &_
        "`Feature_`,`Component_`) VALUES (?,?)") : check
    dim i : for i = 0 to num_components-1
        dim rec : set rec = installer().CreateRecord(2) : check
        rec.stringdata(1) = components(cmp_name, i)
        rec.stringdata(2) = components(cmp_name, i)
        view.execute rec : check
    next
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' enum_files
'
' Enumerate the directories and files in a directory hierarchy.  Use
' the given base to construct directory component names.
'
sub enum_files(base, parent, dir)
    ' enumerate files in this folder
    dim file : for each file in dir.files
        redim preserve files(fil_dims, num_files)
        files(fil_dir, num_files) = num_dirs
        files(fil_name, num_files) = file.name
        files(fil_size, num_files) = fso().GetFile(file.path).Size
        num_files = num_files + 1
    next
    set file = nothing

    ' record this folder
    redim preserve dirs(dir_dims, num_dirs+1)
    dim name : name = dir.path ' dirs(dir_dir, num_dirs) = dir.path
    if (lcase(left(name, len(base))) = lcase(base)) then
        name = mid(name, len(base)+2)
    end if
    if (name = "") then
        name = "TARGETDIR"
        dirs(dir_dir, num_dirs) = "."
        dirs(dir_name, num_dirs) = name
        dirs(dir_parent, num_dirs) = ""
        dirs(dir_default, num_dirs) = "SourceDir"
    else
        dirs(dir_dir, num_dirs) = name
        dim slash : slash = instr(name, "\")
        do while (slash)
            name = left(name, slash-1) & "_" & mid(name, slash+1)
            slash = instr(slash, name, "\")
        loop
        dirs(dir_name, num_dirs) = name
        dirs(dir_parent, num_dirs) = parent
        dirs(dir_default, num_dirs) = wit_filename(dir.name)
    end if
    num_dirs = num_dirs+1

    ' record subfolders
    dim subfolder : for each subfolder in dir.subfolders
        enum_files base, name, subfolder
    next
    set subfolder = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' create_component
'
' Create a unique component just for this file -- used for .exe, .dll, etc.
'
sub create_component(file_idx, name)
    redim preserve components(cmp_dims, num_components)
    components(cmp_name, num_components) = database_key(name)
    components(cmp_dir, num_components) = files(fil_dir, file_idx)
    components(cmp_file_list, num_components) = file_idx
    files(fil_component, file_idx) = num_components
    num_components = num_components+1
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' add_to_component
'
' Look for a directory component with which to associate this file
'
sub add_to_component(base, file_idx)
    ' scan list to see if we have a directory component already
    dim i : for i = num_components - num_dir_components to num_components-1
        if (components(cmp_dir, i) = files(fil_dir, file_idx)) then
            components(cmp_file_list, i) = _
                components(cmp_file_list, i) & "," & file_idx
            files(fil_component, file_idx) = i
            exit sub
        end if
    next
    
    ' didn't find it, so create new directory component
    redim preserve components(cmp_dims, num_components)
    dim compname : compname = dirs(dir_dir, files(fil_dir, file_idx))
    dim slash : slash = instr(compname, "\")
    do while (slash)
        compname = left(compname, slash-1) & "_" & mid(compname, slash+1)
        slash = instr(slash, compname, "\")
    loop
    if (compname = ".") then compname = base & "_Top"
    create_component file_idx, compname
    num_dir_components = num_dir_components + 1
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' determine_components
'
' Scan file list and create a collection of components according to
' Windows Installer component rules:
'   Create components for any .hlp, .dll, .ocx, .exe
'   Remaining files grouped into components by directory.
'   First file selected as key file.
'
sub determine_components(base)
    redim assigned_files(num_files)
    ' scan for special files requiring their own component
    dim i : for i = 0 to num_files-1
        dim ext : ext = lcase(right(files(fil_name, i), 4))
        if (ext = ".exe") or (ext = ".dll") or (ext = ".ocx") or (ext = ".hlp") then
            create_component i, files(fil_name, i)
            assigned_files(i) = num_components
        else
            assigned_files(i) = -1
        end if
    next
    
    ' any files not yet assigned to a component will be dumped into a
    ' component for each directory
    for i = 0 to num_files-1
        if (assigned_files(i) = -1) then
            add_to_component base, i
        end if
    next
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' display_components
'
' Create tree view of components
'
sub display_components(tv)
    dim i : for i = 0 to num_components-1
        tv.Nodes.Add ,, "R" & i, components(cmp_name, i)
        
        dim start : start = 1
        dim finish : finish = instr(start, components(cmp_file_list, i), ",")
        dim file
        do while (finish)
            file = cint(mid(components(cmp_file_list, i), start, finish-start))
            start = finish+1
            finish = instr(start, components(cmp_file_list, i), ",")
            tv.nodes.add "R" & i, tvwChild, "F" & file, files(fil_name, file)
        loop
        file = cint(mid(components(cmp_file_list, i), start))
        tv.nodes.add "R" & i, tvwChild, "F" & file, files(fil_name, file)
    next
    set tv = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' scan_directory
'
' Scan a directory and create components from its contents.
'
sub scan_directory
    num_dirs = 0
    num_files = 0
    num_components = 0    
    num_dir_components = 0

    dim dir : dir = document.forms("CreateComponents").all("dir").value
    dim folder : set folder = fso().GetFolder(dir) : check
    enum_files dir, "", folder
    set folder = nothing
    
    dim base : base = instrrev(dir, "\")
    if (base > 0) then base = mid(dir, base+1) else base = dir
    determine_components base
    
    dim i : for i = 0 to num_dirs-1
        if (lcase(left(dirs(dir_dir, i), len(dir))) = lcase(dir)) then
            dim slash : slash = 0
            if (mid(dirs(dir_dir, i), len(dir)+1, 1) = "\") then slash = 1
            dirs(dir_dir, i) = mid(dirs(dir_dir, i), len(dir)+1+slash)
        end if
    next
    dim tv : set tv = document.forms("CreateComponents").all("ComponentTree")
    tv.nodes.clear
    display_components tv
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub write_tool_input(name)
    write_input "NewComponents", document.forms("CreateComponents").all, name
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub read_tool_input(name, fallback)
    read_input "NewComponents", document.forms("CreateComponents").all, _
        name, fallback
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' add_features
'
' recursive helper routine to add features to a tree view control
'
sub add_features(byref tree, tv, parent_label, parent)
    dim kids : kids = split(tree(parent), ",")
    sort kids
    dim i : for i = 0 to ubound(kids)
        dim label : label = parent_label & ".F" & i
        tv.nodes.add parent_label, tvwChild, label, kids(i)
        add_features tree, tv, label, kids(i)
    next
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' display_features
'
' Load the features of the database into a tree view control.
'
sub display_features
    dim view : set view = exec_view(g_main_frame.g_database, _
        "select `Feature`,`Feature_Parent` from `Feature`" &_
        " order by `Feature_Parent`")
    dim rec : set rec = view.fetch
    dim roots()
    dim tree : set tree = check_object("Scripting.Dictionary")
    if (rec is nothing) then
        exit sub
    end if
    do while (not rec is nothing)
        if (rec.isnull(2)) then
            push roots, rec.stringdata(1)
        else
            dim kids
            if (tree.exists(rec.stringdata(2))) then
                kids = tree(rec.stringdata(2)) & ","
                tree.remove rec.stringdata(2)
            else
                kids = ""
            end if
            tree.add rec.stringdata(2), kids & rec.stringdata(1)
        end if
        set rec = view.fetch
    loop
    view.close : check
    set view = nothing
    set rec = nothing
    
    sort roots
    
    dim tv : set tv = document.all("features")
    tv.nodes.clear
    dim i : for i = 0 to ubound(roots)
        dim label : label = "R" & i
        tv.nodes.add ,, label, roots(i)
        add_features tree, tv, label, roots(i)
    next
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' commit_components
'
' Store File, Directory, Media, Component, Feature and FeatureComponent
' rows in the database from information in the arrays.
'
sub commit_components(db)
    save_file_table db
    save_directory_table db, selected_option(document.all("rootDirectory"))
    save_media_table db
    save_component_table db
    save_feature_table db, document.all("features").selectedItem.Text
    save_feature_components_table db
    db_modified
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' back_next_update
'
' Depending on the page and the inputs on each page, determine whether
' or not the back, next, cancel and finish buttons should be enabled.
'
sub back_next_update
    select case g_page
    case PAGE_DIRECTORY:
        dim dir : dir = document.all("dir").value
        document.all("back").disabled = true
        document.all("next").disabled = (dir = "") or not fso().FolderExists(dir)
        document.all("cancel").disabled = true
        document.all("finish").disabled = true
        
    case PAGE_COMPONENTS:
        document.all("back").disabled = false
        document.all("next").disabled = (num_components = 0)
        document.all("cancel").disabled = false
        document.all("finish").disabled = (num_components = 0)
    
    case PAGE_FEATURES:
        document.all("back").disabled = false
        document.all("next").disabled = _
            (document.all("features").SelectedItem is nothing)
        document.all("cancel").disabled = false
        document.all("finish").disabled = document.all("next").disabled
        
    case PAGE_COMMIT:
        document.all("back").disabled = false
        document.all("next").disabled = true
        document.all("cancel").disabled = false
        document.all("finish").disabled = false
    case else
        MsgBox("Unexpected case on back_next_update")
    end select
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' display_totals
'
' Display the final totals on the last page of the sequence.
'
sub display_totals
    document.all("componentCount").innerText = num_components
    document.all("directoryCount").innerText = num_dirs
    document.all("fileCount").innerText = num_files
    document.all("rootFeature").innerText = document.all("features").selectedItem.text
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' do_step
'
' Do a step in the wizard sequence.
'
sub do_step
    select case g_page
    case PAGE_DIRECTORY:        scan_directory
    case PAGE_COMPONENTS:       display_features
    case PAGE_FEATURES:         display_totals
    case PAGE_LAST:             commit_components(g_main_frame.g_database)
    end select
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' undo_step
'
' Undo a step in the wizard sequence.
'
sub undo_step
    select case g_page
    case PAGE_FEATURES:         display_features
    case PAGE_COMPONENTS:       scan_directory
    end select
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' load_directories
'
' Load the primary keys of the directory table for the new feature form
'
sub load_directories
    dim view : set view = exec_view(g_main_frame.g_database, _
        "select `Directory` from `Directory`")
    dim rec : set rec = view.fetch
    dim dirs()
    do while (not rec is nothing)
        dim dir : dir = rec.stringdata(1)
        if (instr(dir, ".") = 0) then
            push dirs, dir
        end if
        set rec = view.fetch
    loop
    view.close : check
    sort dirs
    load_opts document.all("directory").options, dirs
    load_opts document.all("rootDirectory").options, dirs
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' ComponentTree_AfterLabelEdit
'
' If they changed the label of a root node (a component name), then
' put store new component name
'
sub ComponentTree_AfterLabelEdit(cancel, newstring)
    if (not cancel) then
        dim tv : set tv = _
            document.forms("CreateComponents").all("ComponentTree")
        select case left(tv.SelectedItem.key, 1)
        case "R"
            components(cmp_name, cint(mid(tv.selecteditem.key, 2))) = _
                newstring
        end select
        set tv = nothing
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' ComponentTree_NodeClick
'
' Update the component detail for the currently selected item.
'
sub ComponentTree_NodeClick(node)
    dim target_dir : target_dir = ""
    select case left(node.key, 1)
    case "R" 
        target_dir = dirs(dir_dir, components(cmp_dir, cint(mid(node.key, 2))))
    case "F"
        target_dir = dirs(dir_dir, files(fil_dir, cint(mid(node.key, 2))))
    case else
        MsgBox("Unknown key '" & node.key & "'")
        target_dir = node.key
    end select
    dim source_dir
    if (target_dir = ".") then
        target_dir = "TARGETDIR"
        source_dir = "SourceDir"
    else
        source_dir = "SourceDir\" & target_dir
        target_dir = "TARGETDIR\" & target_dir
    end if
    document.forms("CreateComponents").all("target").innerhtml = target_dir
    document.forms("CreateComponents").all("source").innerhtml = source_dir
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' next_onClick
'
' Move forward one step in the wizard sequence.
'
sub next_onClick
    do_step
    
    if (g_page >= 1) then
        document.all("page" & g_page).className = "invisible"
    end if
    g_page = g_page + 1
    if (g_page <= PAGE_LAST) then
        document.all("page" & g_page).className = ""
    end if
    back_next_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' back_onClick
'
' Move backward one step in the wizard sequence.
'
sub back_onClick
    if (g_page <= PAGE_LAST) then
        document.all("page" & g_page).className = "invisible"
    end if
    g_page = g_page - 1
    if (g_page >= 1) then
        document.all("page" & g_page).className = ""
   end if
   undo_step
   back_next_update 
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' browse_onClick
'
' Browse for a directory to scan for components.
'
sub browse_onClick
    dim dir : dir = browse_for_existing_dir("Scan directory:")
    if (dir <> "") then
        document.all("dir").value = dir
        back_next_update
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' update the back/next buttons based on the changes in these form inputs
sub dir_onChange : back_next_update : end sub
sub dir_onKeyUp  : back_next_update : end sub
sub features_Click : back_next_update : end sub
sub features_KeyUp : back_next_update : end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' cancel_onClick
'
' Cancel the actions of the wizard sequence and reset to the first page.
'
sub cancel_onClick
    document.all("page" & g_page).className = "invisible"
    g_page = 1
    document.all("page" & g_page).className = ""
    back_next_update
end sub
    
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' finish_onClick
'
' Do all the remaining steps, taking the default action, and finish up
' the wizard sequence.
'
sub finish_onClick
    document.all("page" & g_page).className = "invisible"
    do while (g_page < PAGE_LAST)
        do_step
        g_page = g_page+1
    loop
    do_step
    g_page = 1
    document.all("page" & g_page).className = ""
    back_next_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' newFeature_onClick
'
' Using the inputs in the form, add a new feature to the database.
'
sub newFeature_onClick
    dim view : set view = g_main_frame.g_database.openview( _
        "INSERT INTO `Feature`(`Feature`,`Feature_Parent`,`Title`," &_
        "`Description`,`Display`,`Level`,`Directory_`,`Attributes`)" &_
        " VALUES (?,?,?,?,?,?,?,?)") : check
    dim rec : set rec = installer().CreateRecord(8) : check
    with document
        rec.stringdata(1) = .all("name").value
        rec.stringdata(2) = .all("parent").value
        rec.stringdata(3) = .all("title").value
        rec.stringdata(4) = .all("description").value
        rec.integerdata(5) = .all("display").value
        rec.integerdata(6) = .all("level").value
        rec.stringdata(7) = selected_option(.all("directory"))
        rec.integerdata(8) = .all("attributes").value
    end with
    view.execute rec : check
    view.close : check
    
    db_modified
    display_features
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' window_onLoad
'
' Remember persisted scan directory setting.  Load up directories out
' of the database into the GUI comboboxes.
'
sub window_onLoad
    read_tool_input "dir", ""
    load_directories
    back_next_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' window_onUnload
'
' Persist scan directory name into registry.
'
sub window_onUnload
    write_tool_input "dir"
end sub
--></SCRIPT>
</HEAD>
<BODY>

<FORM ID="CreateComponents" NAME="CreateComponents"><TABLE WIDTH=100%>
<TR ID="page1"><TD COLSPAN=3>
<P><STRONG>1. Select Scan Directory:</STRONG></P>

<P><INPUT NAME="dir" TYPE="text" SIZE=30>
<INPUT NAME="browse" TYPE="button" VALUE="..."></P>
</TD></TR>

<TR ID="page2" CLASS="invisible"><TD COLSPAN=3>
<P><STRONG>2. Modify Generated Components</STRONG></P>
<P><TABLE>
    <THEAD>
        <TR><TH>Components:</TH><TH>Component Detail:</TH></TR>
    </THEAD>
    <TBODY>
        <TR><TD><OBJECT classid=clsid:C74190B6-8589-11D1-B16A-00C0F0283628 height=250 id=ComponentTree style="HEIGHT: 250px; WIDTH: 241px" VIEWASTEXT><PARAM NAME="_ExtentX" VALUE="6376"><PARAM NAME="_ExtentY" VALUE="6615"><PARAM NAME="_Version" VALUE="393217"><PARAM NAME="HideSelection" VALUE="1"><PARAM NAME="Indentation" VALUE="423"><PARAM NAME="LabelEdit" VALUE="0"><PARAM NAME="LineStyle" VALUE="1"><PARAM NAME="PathSeparator" VALUE="\"><PARAM NAME="Sorted" VALUE="1"><PARAM NAME="Style" VALUE="7"><PARAM NAME="Checkboxes" VALUE="0"><PARAM NAME="FullRowSelect" VALUE="0"><PARAM NAME="HotTracking" VALUE="0"><PARAM NAME="Scroll" VALUE="1"><PARAM NAME="SingleSel" VALUE="0"><PARAM NAME="ImageList" VALUE=""><PARAM NAME="BorderStyle" VALUE="0"><PARAM NAME="Appearance" VALUE="1"><PARAM NAME="MousePointer" VALUE="0"><PARAM NAME="Enabled" VALUE="1"><PARAM NAME="OLEDragMode" VALUE="0"><PARAM NAME="OLEDropMode" VALUE="0"></OBJECT></TD>
            <TD VALIGN=top><TABLE>
                <TBODY>
                <TR><TH>Source Directory:</TH><TD ID="source"></TD></TR>
                <TR><TH>Target Directory:</TH><TD ID="target"></TD></TR>
                </TBODY>
            </TABLE></TD>
        </TR>
    </TBODY>
</TABLE></P>
</TD></TR>

<TR ID="page3" CLASS="invisible"><TD COLSPAN=3>
<P><STRONG>3. Locate New Components Under a Feature and Directory</STRONG></P>

<P><TABLE><TR>
<TD><SELECT ID="rootDirectory" SIZE=1 STYLE="width: 250px"></SELECT><BR>
<OBJECT classid=clsid:C74190B6-8589-11D1-B16A-00C0F0283628 height=250 id="features" name="features" style="HEIGHT: 250px; WIDTH: 241px" VIEWASTEXT><PARAM NAME="_ExtentX" VALUE="6376"><PARAM NAME="_ExtentY" VALUE="6615"><PARAM NAME="_Version" VALUE="393217"><PARAM NAME="HideSelection" VALUE="1"><PARAM NAME="Indentation" VALUE="423"><PARAM NAME="LabelEdit" VALUE="1"><PARAM NAME="LineStyle" VALUE="1"><PARAM NAME="PathSeparator" VALUE="\"><PARAM NAME="Sorted" VALUE="1"><PARAM NAME="Style" VALUE="7"><PARAM NAME="Checkboxes" VALUE="0"><PARAM NAME="FullRowSelect" VALUE="0"><PARAM NAME="HotTracking" VALUE="0"><PARAM NAME="Scroll" VALUE="1"><PARAM NAME="SingleSel" VALUE="0"><PARAM NAME="ImageList" VALUE=""><PARAM NAME="BorderStyle" VALUE="0"><PARAM NAME="Appearance" VALUE="1"><PARAM NAME="MousePointer" VALUE="0"><PARAM NAME="Enabled" VALUE="1"><PARAM NAME="OLEDragMode" VALUE="0"><PARAM NAME="OLEDropMode" VALUE="0"></OBJECT>
</TD>
<TD VALIGN="top"><TABLE>
    <TR>
        <TD ALIGN="right"><STRONG>Name:</STRONG></TD>
        <TD><INPUT ID="name" TYPE="text" SIZE=15></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Parent:</STRONG></TD>
        <TD><INPUT ID="parent" TYPE="text" SIZE=15></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Title:</STRONG></TD>
        <TD><INPUT ID="title" TYPE="text" SIZE=15></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Description:</STRONG></TD>
        <TD><INPUT ID="description" TYPE="text" SIZE=30></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Display:</STRONG></TD>
        <TD><INPUT ID="display" TYPE="text" SIZE=5> (integer)</TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Level:</STRONG></TD>
        <TD><INPUT ID="level" TYPE="text" SIZE=5> (integer)</TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Directory:</STRONG></TD>
        <TD><SElECT ID="directory" SIZE=1></SELECT></TD>
    </TR>
    <TR>
        <TD ALIGN="right"><STRONG>Attributes:</STRONG></TD>
        <TD><INPUT ID="attributes" TYPE="text" SIZE=5> (integer)</TD>
    </TR>
    <TR>
        <TD> </TD>
        <TD><INPUT ID="newFeature" TYPE="button" VALUE="New..."></TD>
    </TR>
</TABLE></TD>
</TR></TABLE></P>

<TR ID="page4" CLASS="invisible"><TD COLSPAN=3>
<P><STRONG>4. Commit Components to the Database</STRONG></P>
<P>Commit <SPAN ID="componentCount"></SPAN>&nbsp;components,
<SPAN ID="directoryCount"></SPAN>&nbsp;directories and
<SPAN ID="fileCount"></SPAN>&nbsp;files under parent feature
'<SPAN ID="rootFeature"></SPAN>'?</P>
</TD></TR>

<P><TR>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="back" TYPE="button" VALUE="< Back" DISABLED></TD>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="next" TYPE="button" VALUE="Next >" DISABLED></TD>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="cancel" TYPE="button" VALUE="Cancel" DISABLED>
<INPUT ID="finish" TYPE="button" VALUE="Finish" DISABLED></TD>
</TR></P>
</TABLE></FORM>

<P ID="debug"></P>

</BODY>
</HTML>
