<HTML>
<HEAD>
<!--
    izfree Tools for Windows Installer 1.1
    Copyright (C) 2001 Pahvant Technologies, Inc.

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
-->
<META name=VI60_defaultClientScript content=VBScript>
<META NAME="GENERATOR" Content="Microsoft Visual Studio 6.0">
<TITLE>Scan Directory for Components</TITLE>
<LINK REL="stylesheet" HREF="izfree.css">
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript" SRC="izfree.vbs"></SCRIPT>
<SCRIPT TYPE="text/vbscript" LANGUAGE="vbscript"><!--
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
option explicit

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim g_main_frame

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
const PAGE_DIRECTORY = 1
const PAGE_COMPONENTS = 2
const PAGE_FEATURES = 3
const PAGE_COMMIT = 4
const PAGE_LAST = 4 

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim g_page : g_page = 1

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim num_files : dim files()
const fil_dir = 0
const fil_name = 1
const fil_component = 2
const fil_size = 3
const fil_dims = 3

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim num_dirs : dim dirs()
const dir_dir = 0
const dir_name = 1
const dir_parent = 2
const dir_default = 3
const dir_dims = 3

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
dim num_components : dim components()
const cmp_name = 0
const cmp_dir = 1
const cmp_file_list = 2
const cmp_dims = 2
dim num_dir_components

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_component_table
'
' Write Component table into database.
'
sub save_component_table(db)
    dim view : set view = db.openview("INSERT INTO `Component`(" &_
        "`Component`,`ComponentId`,`Directory_`,`Attributes`," &_
        "`Condition`,`KeyPath`) VALUES (?,?,?,0,'','')") : check
    dim digits : digits = sig_digits(num_components)
    dim i : for i = 0 to num_components-1
        dim rec : set rec = installer().CreateRecord(3) : check
        rec.stringdata(1) = components(cmp_name, i)
        rec.stringdata(2) = new_guid()
        rec.stringdata(3) = dirs(dir_name, components(cmp_dir, i))
        view.execute rec : check : if err.number then exit sub
        set rec = nothing
    next
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_file_table
'
' Write File table into database.
'
sub save_file_table(db)
    dim view : set view = db.OpenView("INSERT INTO `File`(" & _
            "`File`,`Component_`,`FileName`,`FileSize`,`Version`," & _
            "`Language`,`Attributes`,`Sequence`) " & _
            "VALUES (?,?,?,?,'','',0,1)") : check

    dim digits : digits = sig_digits(num_files)
    dim i : for i = 0 to num_files-1
        dim rec : set rec = installer().CreateRecord(4) : check
        rec.stringdata(1) = "f" & right("000000" & i, digits) & "_" & files(fil_name, i)
        rec.stringdata(2) = components(cmp_name, files(fil_component, i))
        rec.stringdata(3) = wit_filename(files(fil_name, i))
        rec.integerdata(4) = files(fil_size, i)
        view.execute rec : check : if err.number then exit sub
        set rec = nothing
    next
    
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_directory_table
'
' Write Directory table into database.
'
sub save_directory_table(db)
    dim view : set view = db.openview("INSERT INTO `Directory`(" &_
        "`Directory`,`Directory_Parent`,`DefaultDir`) VALUES (?,?,?)") : check
    dim i : for i = 0 to num_dirs-1
        dim rec : set rec = installer().CreateRecord(3) : check
        rec.stringdata(1) = dirs(dir_name, i)
        rec.stringdata(2) = dirs(dir_parent, i)
        rec.stringdata(3) = dirs(dir_default, i)
        view.execute rec : check : if err.number then exit sub
        set rec = nothing
    next
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_media_table
'
' Write Media table into database.
'
sub save_media_table(db)
    dim view : set view = db.openview("INSERT INTO `Media`(`DiskId`,`LastSequence`,`DiskPrompt`,`Cabinet`,`VolumeLabel`,`Source`) " &_
        "VALUES (1,1,'','','','')") : check
    view.execute : check
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_feature_table
'
' Write Feature table into database.
sub save_feature_table(db, root, make_root)
    dim view : set view = db.openview("INSERT INTO `Feature`(" &_
        "`Feature`,`Feature_Parent`,`Title`,`Description`,`Display`,`Level`,`Directory_`,`Attributes`) " &_
        "VALUES (?,?,?,?,?,?,?,0)") : check
    dim i : for i = 0 to num_components-1
        dim rec : set rec = installer().CreateRecord(7) : check
        rec.stringdata(1) = components(cmp_name, i) 'feature
        rec.stringdata(2) = root                    'parent
        rec.stringdata(3) = components(cmp_name, i) 'title
        rec.stringdata(4) = components(cmp_name, i) 'description
        rec.integerdata(5) = 2*(i+100)              'display
        rec.integerdata(6) = 1                      'level
        rec.stringdata(7) = "" 'dirs(dir_name, components(cmp_dir, i)) 'directory
        view.execute rec : check
        set rec = nothing
    next
    if (make_root) then
        set rec = installer().CreateRecord(7) : check
        rec.stringdata(1) = "All"
        rec.stringdata(2) = ""
        rec.stringdata(3) = "All"
        rec.stringdata(4) = "All Features"
        rec.integerdata(5) = 10
        rec.integerdata(6) = 1
        rec.stringdata(7) = "TARGETDIR"
        view.execute rec : check
    end if
    view.close : check
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' save_feature_components_table
'
' Write FeatureComponents table into database.
'
sub save_feature_components_table(db)
    dim view : set view = db.openview("INSERT INTO `FeatureComponents`(" &_
        "`Feature_`,`Component_`) VALUES (?,?)") : check
    dim i : for i = 0 to num_components-1
        dim rec : set rec = installer().CreateRecord(2) : check
        rec.stringdata(1) = components(cmp_name, i)
        rec.stringdata(2) = components(cmp_name, i)
        view.execute rec : check
    next
    view.close : check
    set view = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' enum_files
'
' Enumerate the directories and files in a directory hierarchy
'
sub enum_files(base, parent, dir)
    ' enumerate files in this folder
    dim file : for each file in dir.files
        redim preserve files(fil_dims, num_files)
        files(fil_dir, num_files) = num_dirs
        files(fil_name, num_files) = file.name
        files(fil_size, num_files) = fso().GetFile(file.path).Size
        num_files = num_files + 1
    next
    set file = nothing

    ' record this folder
    redim preserve dirs(dir_dims, num_dirs+1)
    dim name : name = dir.path ' dirs(dir_dir, num_dirs) = dir.path
    if (lcase(left(name, len(base))) = lcase(base)) then
        name = mid(name, len(base)+2)
    end if
    if (name = "") then
        name = "TARGETDIR"
        dirs(dir_dir, num_dirs) = "."
        dirs(dir_name, num_dirs) = name
        dirs(dir_parent, num_dirs) = ""
        dirs(dir_default, num_dirs) = "SourceDir"
    else
        dirs(dir_dir, num_dirs) = name
        dim slash : slash = instr(name, "\")
        do while (slash)
            name = left(name, slash-1) & "_" & mid(name, slash+1)
            slash = instr(slash, name, "\")
        loop
        dirs(dir_name, num_dirs) = name
        dirs(dir_parent, num_dirs) = parent
        dirs(dir_default, num_dirs) = wit_filename(dir.name)
    end if
    num_dirs = num_dirs+1

    ' record subfolders
    dim subfolder : for each subfolder in dir.subfolders
        enum_files base, name, subfolder
    next
    set subfolder = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' create_component
'
' Create a unique component just for this file -- used for .exe, .dll, etc.
'
sub create_component(file_idx, name)
    redim preserve components(cmp_dims, num_components)
    components(cmp_name, num_components) = name
    components(cmp_dir, num_components) = files(fil_dir, file_idx)
    components(cmp_file_list, num_components) = file_idx
    files(fil_component, file_idx) = num_components
    num_components = num_components+1
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' add_to_component
'
' Look for a directory component with which to associate this file
'
sub add_to_component(file_idx)
    ' scan list to see if we have a directory component already
    dim i : for i = num_components - num_dir_components to num_components-1
        if (components(cmp_dir, i) = files(fil_dir, file_idx)) then
            components(cmp_file_list, i) = _
                components(cmp_file_list, i) & "," & file_idx
            files(fil_component, file_idx) = i
            exit sub
        end if
    next
    
    ' didn't find it, so create new directory component
    redim preserve components(cmp_dims, num_components)
    dim compname : compname = dirs(dir_dir, files(fil_dir, file_idx))
    dim slash : slash = instr(compname, "\")
    do while (slash)
        compname = left(compname, slash-1) & "_" & mid(compname, slash+1)
        slash = instr(slash, compname, "\")
    loop
    if (compname = ".") then compname = "Top"
    create_component file_idx, compname
    num_dir_components = num_dir_components + 1
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' determine_components
'
' Scan file list and create a collection of components according to
' Windows Installer component rules:
'   Create components for any .hlp, .dll, .ocx, .exe
'   Remaining files grouped into components by directory.
'   First file selected as key file.
'
sub determine_components
    redim assigned_files(num_files)
    ' scan for special files requiring their own component
    dim i : for i = 0 to num_files-1
        dim ext : ext = lcase(right(files(fil_name, i), 4))
        if (ext = ".exe") or (ext = ".dll") or (ext = ".ocx") or (ext = ".hlp") then
            create_component i, files(fil_name, i)
            assigned_files(i) = num_components
        else
            assigned_files(i) = -1
        end if
    next
    
    ' any files not yet assigned to a component will be dumped into a
    ' component for each directory
    for i = 0 to num_files-1
        if (assigned_files(i) = -1) then
            add_to_component(i)
        end if
    next
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' display_components
'
' Create tree view of components
'
sub display_components(tv)
    dim i : for i = 0 to num_components-1
        tv.Nodes.Add ,, "R" & i, components(cmp_name, i)
        
        dim start : start = 1
        dim finish : finish = instr(start, components(cmp_file_list, i), ",")
        dim file
        do while (finish)
            file = cint(mid(components(cmp_file_list, i), start, finish-start))
            start = finish+1
            finish = instr(start, components(cmp_file_list, i), ",")
            tv.nodes.add "R" & i, tvwChild, "F" & file, files(fil_name, file)
        loop
        file = cint(mid(components(cmp_file_list, i), start))
        tv.nodes.add "R" & i, tvwChild, "F" & file, files(fil_name, file)
    next
    set tv = nothing
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' update_create_components
'
' Set UI state based on current values
'
sub update_create_components
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' scan_directory
'
sub scan_directory
    num_dirs = 0
    num_files = 0
    num_components = 0    
    num_dir_components = 0

    dim dir : dir = document.forms("CreateComponents").all("dir").value
    dim folder : set folder = fso().GetFolder(dir) : check
    enum_files dir, "", folder
    set folder = nothing
    
    determine_components
    
    dim i : for i = 0 to num_dirs-1
        if (lcase(left(dirs(dir_dir, i), len(dir))) = lcase(dir)) then
            dim slash : slash = 0
            if (mid(dirs(dir_dir, i), len(dir)+1, 1) = "\") then slash = 1
            dirs(dir_dir, i) = mid(dirs(dir_dir, i), len(dir)+1+slash)
        end if
    next
    dim tv : set tv = document.forms("CreateComponents").all("ComponentTree")
    tv.nodes.clear
    display_components tv
    update_create_components
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' ComponentTree_AfterLabelEdit
'
' If they changed the label of a root node (a component name), then
' put store new component name
'
sub ComponentTree_AfterLabelEdit(cancel, newstring)
    if (not cancel) then
        dim tv : set tv = document.forms("CreateComponents").all("ComponentTree")
        select case left(tv.SelectedItem.key, 1)
        case "R"
            components(cmp_name, cint(mid(tv.selecteditem.key, 2))) = newstring
        end select
        set tv = nothing
    end if
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' ComponentTree_NodeClick
'
' Update the component detail for the currently selected item.
'
sub ComponentTree_NodeClick(node)
    dim target_dir : target_dir = ""
    select case left(node.key, 1)
    case "R" 
        target_dir = dirs(dir_dir, components(cmp_dir, cint(mid(node.key, 2))))
    case "F"
        target_dir = dirs(dir_dir, files(fil_dir, cint(mid(node.key, 2))))
    case else
        MsgBox("Unknown key '" & node.key & "'")
        target_dir = node.key
    end select
    dim source_dir
    if (target_dir = ".") then
        target_dir = "TARGETDIR"
        source_dir = "SourceDir"
    else
        source_dir = "SourceDir\" & target_dir
        target_dir = "TARGETDIR\" & target_dir
    end if
    document.forms("CreateComponents").all("target").innerhtml = target_dir
    document.forms("CreateComponents").all("source").innerhtml = source_dir
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub write_tool_input(name)
    write_input "NewComponents", document.forms("CreateComponents").all, name
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub read_tool_input(name, fallback)
    read_input "NewComponents", document.forms("CreateComponents").all, name, _
        fallback
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onLoad
    set g_main_frame = window.parent
    read_tool_input "dir", ""
    update_scan
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub window_onUnload
    write_tool_input "dir"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub help_onClick
    show_help "NewComponents"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub update_scan
    dim val : val = document.all("dir").value
    dim enabled : enabled = (val <> "") and fso().FolderExists(val)
    document.all("next").disabled = not enabled
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub dir_onChange : update_scan : end sub
sub dir_onKeyUp  : update_scan : end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub display_features
    dbgout "Display features"
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
' commit_components
'
' Store File, Directory, Media, Component, Feature and FeatureComponent
' rows in the database from information in the arrays.
'
sub commit_components(db)
    save_file_table db
    save_directory_table db
    save_media_table db
    save_component_table db
    save_feature_table db, "All", true
    save_feature_components_table db
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub back_next_update
    dim first : first = (g_page = 1)
    dim last : last = (g_page = PAGE_LAST)
    dim no_comps : no_comps = (num_components = 0)
    document.all("back").disabled = first
    document.all("next").disabled = last or (not first and no_comps)
    dim disabled : disabled = no_comps or first
    document.all("finish").disabled = disabled
    document.all("cancel").disabled = disabled
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub do_step
    select case g_page
    case PAGE_DIRECTORY:        scan_directory
    case PAGE_COMPONENTS:       display_features
    case PAGE_FEATURES:
    case PAGE_LAST:             commit_components
    end select
end sub

sub next_onClick
    do_step
    
    if (g_page >= 1) then
        document.all("page" & g_page).className = "invisible"
    end if
    g_page = g_page + 1
    if (g_page <= PAGE_LAST) then
        document.all("page" & g_page).className = ""
    end if
    back_next_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub back_onClick
    if (g_page <= PAGE_LAST) then
        document.all("page" & g_page).className = "invisible"
    end if
    g_page = g_page - 1
    if (g_page >= 1) then
        document.all("page" & g_page).className = ""
   end if
   back_next_update
end sub

''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
sub browse_onClick
    dim dir : dir = browse_for_existing_dir("Output directory:")
    if (dir <> "") then
        document.all("dir").value = dir
    end if
end sub

sub cancel_onClick
    document.all("page" & g_page).className = "invisible"
    g_page = 1
    document.all("page" & g_page).className = ""
    back_next_update
end sub
    
sub finish_onClick
    document.all("page" & g_page).className = "invisible"
    do while (g_page < PAGE_LAST)
        do_step
        g_page = g_page+1
    loop
    g_page = 1
    document.all("page" & g_page).className = ""
    back_next_update
end sub

--></SCRIPT>
</HEAD>
<BODY>

<FORM NAME="CreateComponents"><TABLE WIDTH=100%>
<TR ID="page1"><TD COLSPAN=3>
<P><STRONG>1. Select Scan Directory:</STRONG></P>

<P><INPUT NAME="dir" TYPE="text" SIZE=30>
<INPUT NAME="browse" TYPE="button" VALUE="..."></P>
</TD></TR>

<TR ID="page2" CLASS="invisible"><TD COLSPAN=3>
<P><STRONG>2. Modify Generated Components</STRONG></P>
<P><TABLE>
    <THEAD>
        <TR><TH>Components:</TH><TH>Component Detail:</TH></TR>
    </THEAD>
    <TBODY>
        <TR><TD><OBJECT classid=clsid:C74190B6-8589-11D1-B16A-00C0F0283628 height=250 id=ComponentTree style="HEIGHT: 250px; WIDTH: 241px" VIEWASTEXT><PARAM NAME="_ExtentX" VALUE="6376"><PARAM NAME="_ExtentY" VALUE="6615"><PARAM NAME="_Version" VALUE="393217"><PARAM NAME="HideSelection" VALUE="1"><PARAM NAME="Indentation" VALUE="423"><PARAM NAME="LabelEdit" VALUE="0"><PARAM NAME="LineStyle" VALUE="1"><PARAM NAME="PathSeparator" VALUE="\"><PARAM NAME="Sorted" VALUE="1"><PARAM NAME="Style" VALUE="7"><PARAM NAME="Checkboxes" VALUE="0"><PARAM NAME="FullRowSelect" VALUE="0"><PARAM NAME="HotTracking" VALUE="0"><PARAM NAME="Scroll" VALUE="1"><PARAM NAME="SingleSel" VALUE="0"><PARAM NAME="ImageList" VALUE=""><PARAM NAME="BorderStyle" VALUE="0"><PARAM NAME="Appearance" VALUE="1"><PARAM NAME="MousePointer" VALUE="0"><PARAM NAME="Enabled" VALUE="1"><PARAM NAME="OLEDragMode" VALUE="0"><PARAM NAME="OLEDropMode" VALUE="0"></OBJECT></TD>
            <TD VALIGN=top><TABLE>
                <TBODY>
                <TR><TH>Source Directory:</TH><TD ID="source"></TD></TR>
                <TR><TH>Target Directory:</TH><TD ID="target"></TD></TR>
                </TBODY>
            </TABLE></TD>
        </TR>
    </TBODY>
</TABLE></P>
</TD></TR>

<TR ID="page3" CLASS="invisible"><TD COLSPAN=3>
<P><STRONG>3. Locate New Components Under a Feature</STRONG></P>
<P><OBJECT classid=clsid:C74190B6-8589-11D1-B16A-00C0F0283628 height=250 id="features" style="HEIGHT: 250px; WIDTH: 241px" VIEWASTEXT><PARAM NAME="_ExtentX" VALUE="6376"><PARAM NAME="_ExtentY" VALUE="6615"><PARAM NAME="_Version" VALUE="393217"><PARAM NAME="HideSelection" VALUE="1"><PARAM NAME="Indentation" VALUE="423"><PARAM NAME="LabelEdit" VALUE="0"><PARAM NAME="LineStyle" VALUE="1"><PARAM NAME="PathSeparator" VALUE="\"><PARAM NAME="Sorted" VALUE="1"><PARAM NAME="Style" VALUE="7"><PARAM NAME="Checkboxes" VALUE="0"><PARAM NAME="FullRowSelect" VALUE="0"><PARAM NAME="HotTracking" VALUE="0"><PARAM NAME="Scroll" VALUE="1"><PARAM NAME="SingleSel" VALUE="0"><PARAM NAME="ImageList" VALUE=""><PARAM NAME="BorderStyle" VALUE="0"><PARAM NAME="Appearance" VALUE="1"><PARAM NAME="MousePointer" VALUE="0"><PARAM NAME="Enabled" VALUE="1"><PARAM NAME="OLEDragMode" VALUE="0"><PARAM NAME="OLEDropMode" VALUE="0"></OBJECT></TD>
</P></TD></TR>

<TR ID="page4" CLASS="invisible"><TD COLSPAN=3>
<P><STRONG>4. Commit Components to the Database</STRONG></P>
<P>Commit xx features, yy components and zz files under parent feature 'All'?</P>
</TD></TR>
<TR>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="back" TYPE="button" VALUE="< Back" DISABLED></TD>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="next" TYPE="button" VALUE="Next >" DISABLED></TD>
<TD STYLE="width: 150px" ALIGN="center">
<INPUT ID="cancel" TYPE="button" VALUE="Cancel" DISABLED>
<INPUT ID="finish" TYPE="button" VALUE="Finish" DISABLED></TD>
</TR>
</TABLE></FORM></P>

<P ID="debug"></P>

</BODY>
</HTML>
